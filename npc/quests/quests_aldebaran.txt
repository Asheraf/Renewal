//================== RagEmu Script ========================================
//=     ______            _____                
//=     | ___ \          |  ___|               
//=     | |_/ /__ _  __ _| |__ _ __ ___  _   _ 
//=     |    // _` |/ _` |  __| '_ ` _ \| | | |
//=     | |\ \ (_| | (_| | |__| | | | | | |_| |
//=     \_| \_\__,_|\__, \____/_| |_| |_|\__,_|
//=                  __/ |                     
//=                 |___/                      
//=
//================= License ===============================================
//= This file is part of RagEmu.
//= http://ragemu.org - https://github.com/RagEmu/Renewal
//=
//= Copyright (C) 2016  RagEmu Dev Team
//= Copyright (C)  Ridley
//= Copyright (C)  rAthena Dev Team
//= Copyright (C)  L0ne_W0lf
//= Copyright (C)  Silent
//= Copyright (C)  Lupus
//=
//= RagEmu is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= Quest NPCs related to Aldebaran
//================= Description ===========================================
//= 'Doctor Band', 'Feather Bonnet', 'Opera Masque', 'Sakkat Hat' Quests.
//================= Current Version =======================================
//= 1.3
//=========================================================================

//== 'Doctor Band', 'Feather Bonnet', 'Opera Masque', 'Sakkat Hat' Quest =
aldeba_in,152,166,4	script	Trader#01	4_M_04,{
	mes "[Trader]";
	mes "Who is this mysterious man?";
	mes "I, the enigmatic and debonair 'Trader?'";
	next;
	mes "[Trader]";
	mes "Traveling about the Rune-Midgard Continent, with all of his wonderful hats? Nobody knows...";
	next;
	mes "[Trader]";
	mes "For what purpose do I collect and trade these hats from all around the world? Choose a hat, get closer to unraveling the mystery...";
	next;
	mes " (1) ^3355FFDoctor Band^000000:";
	mes "1 Red Bandana + 50 Irons + 1 Cracked Diamond + 3500 Zeny";
	mes " (2)^3355FFFeather Bonnet^000000:";
	mes "1 Romantic Gent + 300 Feather of Birds + 500 Zeny";
	mes " (3) ^3355FFPhantom of Opera^000000:";
	mes "20 Iron + 1 Singing Plant + 5000 Zeny ";
	mes " (4) ^3355FFSakkat^000000:";
	mes "120 Trunks + 10000 Zeny ";
	next;
	switch(select(" Doctor Band ", " Feather Bonnet ", " Phantom of Opera ", " Sakkat ")) {
	case 1:
		if ((countitem(Red_Bandana) > 0) && (countitem(Iron) > 49) && (countitem(Crystal_Jewel___) > 0) && (Zeny > 3499)) {
			delitem Red_Bandana,1;
			delitem Iron,50;
			delitem Crystal_Jewel___,1;
			Zeny -= 3500;
			mes "[Trader]";
			mes "Hm! You don't have a medical license, do you? It's alright, I've heard about a rogue, unlicensed physician who performed medical miracles! But... That might have been a comic book.";
			next;
			mes "[Trader]";
			mes "Oh whatever. Just don't get caught.";
			getitem Doctor_Cap,1;
			close;
		}
		else {
			mes "[Trader]";
			mes "You guy~";
			mes "Check the requirements again.";
			mes "You don't look like an idiot though. So c'mon man, get real.";
			close;
		}
	case 2:
		if ((countitem(Oldmans_Romance) > 0) && (countitem(Feather_Of_Birds) > 299) && (Zeny > 499)) {
			delitem Oldmans_Romance,1;
			delitem Feather_Of_Birds,300;
			Zeny -= 500;
			mes "[Trader]";
			mes "Ooh~! You have good fashion sense. I know you've had a hard time collecting this stuff, but this hat is worth it. Take it. All you need now is a fur coat and a cane!";
			getitem Feather_Bonnet,1;
			close;
		}
		else {
			mes "[Trader]";
			mes "You guy~";
			mes "Go check my requirements again. You don't look like an idiot though. C'mon man, get real.";
			close;
		}
	case 3:
		if ((countitem(Iron) > 19) && (countitem(Singing_Plant) > 0) && (Zeny > 4999)){ //Iron Singing_Plant
			delitem Iron,20;
			delitem Singing_Plant,1;
			Zeny -= 5000;
			mes "[Trader]";
			mes "This one? It's a little bit scary, though it has some sort of romantic quality. What do you think? You like it? Alright, take it, it's yours!";
			getitem Phantom_Of_Opera,1;
			close;
		}
		else {
			mes "[Trader]";
			mes "Buffoon. Go check the requirements again. ^3355FFPhantom of Opera^000000 isn't easy to come by. So c'mon man, get real.";
			close;
		}
	case 4:
		if ((countitem(Wooden_Block) > 119) && (Zeny > 9999)){ //Wooden_Block
			delitem Wooden_Block,120;
			Zeny -= 10000;
			mes "[Trader]";
			mes "If you have a chance to visit the Uplander Village, Payon, please go and meet the Sakkat Craftsman.";
			next;
			mes "[Trader]";
			mes "He's never sold Sakkat to Traders other than me, since only I can recognize its quality. Due to its rarity, Sakkat has become a very unique and exceptional product. Okay! Take it, it's yours!";
			getitem Sahkkat,1;
			close;
		}
		else {
			mes "[Trader]";
			mes "MORON~ Check my requirements again. C'mon man, you don't look like an idiot, so get real~";
			close;
		}
	}
}

c_tower1,232,222,5	script	Belljamin Button#ct	4_M_RUSMAN2,{
	mes("[Belljamin Button]");
	mes("The monsters here are able to control time.");
	if (BaseLevel < 150) {
		mes("Come back when you've reached ^0000FFLevel 150^000000.");
		close;
	}
	mes("Time, memories, environments... all can become warped here.");
	next;
	mes("[Belljamin Button]");
	mes("I'm begging you not to ask more about it. It might put your life in danger.");
	next;
	.@menu = select("Don't ask", "Go to 2nd floor of warped tower", "Go to 3rd floor of warped tower");
	switch (.@menu) {
	case 1:
		break;
	case 2:
	case 3:
		if (.@menu == 2) {
			.@map$ = "c_tower2_";
			.@x = 268;
			.@y = 26;
		} else {
			.@map$ = "c_tower3_";
			.@x = 65;
			.@y = 147;
		}
		mes("[Belljamin Button]");
		mes("Be careful with your choice.");
		next;
		mes("[Belljamin Button]");
		mes("I need ^0000FF10,000z^000000 to let you in. It's a pretty dangerous place to be.");
		next;
		switch (select("Quit", "Pay 10,000z", "Why do you need money?")) {
		case 1:
			break;
		case 2:
			if (Zeny < 10000) {
				mes("[Belljamin Button]");
				mes("Really? You don't have money and asked me to take you there?");
				close;
			}
			Zeny -= 10000;
			mes("[Belljamin Button]");
			mes("I'm not sure how you'll keep track of time in there but be as careful as you can.");
			close2;
			warp .@map$, .@x, .@y;
			end;
			break;
		case 3:
			mes("[Belljamin Button]");
			mes("I am here all day for the safety of adventurers. I always alert them about the danger. That's why I need some pay.");
			emotion e_swt;
			close;
		}
		break;
	}
	mes("[Belljamin Button]");
	mes("Phew...You've made good choice. The place is so brutal so you should probably get back to a safe area.");
	close;
}

c_tower2_,274,26,3	script	Belljamin Button#nct1	4_M_RUSCHILD,{
	mes("[Belljamin Button]");
	mes("Perhaps you've seen me before?");
	next;
	mes("[Belljamin Button]");
	mes("You saw me in the different place, different look.");
	next;
	switch (select("Who are you?", "Get me out of here!")) {
	case 1:
		mes("[Belljamin Button]");
		mes("Can you to explain me about what you've seen, what you've done?");
		next;
		mes("[Belljamin Button]");
		mes("Me too. You might also be a missing one. Unaware, with no purpose nor idea.");
		next;
		mes("[Belljamin Button]");
		mes("It's time to go back. Please cherish your time more.");
		next;
		switch (select("Stay a little bit more", "Please take me back")) {
		case 1:
			mes("[Belljamin Button]");
			mes("You must really like this place. Well, do whatever you want.");
			close;
		case 2:
			mes("[Belljamin Button]");
			mes("Imagine your favourite drink in a cup. Slowly stir the cup with a spoon, and peer through as the drink change colour.");
			close2;
			warp "c_tower1", 235, 223;
			end;
		}
		break;
	case 2:
		mes("[Belljamin Button]");
		mes("It's time to move on. Do you have a favourite drink?");
		next;
		mes("[Belljamin Button]");
		mes("Think about the drink in a cup. Slowly stir the cup with a spoon, and peer through as the drink change colour.");
		close2;
		warp "c_tower1", 235, 223;
		end;
	}
}

c_tower3_,59,148,5	duplicate(Belljamin Button#nct1)	Belljamin Button#nct2	4_M_BARYO_OLD
