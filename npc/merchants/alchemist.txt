//================== RagEmu Script ========================================
//=     ______            _____                
//=     | ___ \          |  ___|               
//=     | |_/ /__ _  __ _| |__ _ __ ___  _   _ 
//=     |    // _` |/ _` |  __| '_ ` _ \| | | |
//=     | |\ \ (_| | (_| | |__| | | | | | |_| |
//=     \_| \_\__,_|\__, \____/_| |_| |_|\__,_|
//=                  __/ |                     
//=                 |___/                      
//=
//================= License ===============================================
//= This file is part of RagEmu.
//= http://ragemu.org - https://github.com/RagEmu/Renewal
//=
//= Copyright (C) 2016  RagEmu Dev Team
//= Copyright (C)  rAthena Dev Team
//= Copyright (C)  Euphy
//= Copyright (C)  eAthena Dev Team
//= Copyright (C)  Masao
//= Copyright (C)  L0ne_W0lf
//= Copyright (C)  SinSloth
//= Copyright (C)  Lupus
//= Copyright (C)  MasterOfMuppets
//=
//= RagEmu is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= Alchemist Shop
//================= Description ===========================================
//= Merchants for Alchemist Materials and Manuals.
//================= Current Version =======================================
//= 1.9
//=========================================================================

alde_alche,24,188,3	script	Guild Dealer	2_M_ALCHE,{
	if (checkweight(Knife,1) == 0) {
		mes "- Wait a minute! -";
		mes "- Currently you are carrying -";
		mes "- too many items with you. -";
		mes "- Please come back again -";
		mes "- after you store some items into kafra storage. -";
		close;
	}
	mes "[Gever Al Sharp]";
	mes "Welcome to the";
	mes "Alchemist Union.";
	mes "How can I assist you today?";
	next;
	switch (select("Purchase materials.", "Purchase a production manual.", "Quit.")) {
	case 1:
		mes "[Gever Al Sharp]";
		mes "What would you like?";
		next;
		if (select("Medicine Bowl - " + (RENEWAL ? "250z" : "8 Zeny"), "Cancel.") == 1) {
			mes "[Gever Al Sharp]";
			mes "How many do you want?";
			mes "Enter '0' if you want to quit.";
			next;
			while(1) {
				input .@input, 0, 2001;
				if (.@input == 0) {
					mes "[Gever Al Sharp]";
					mes "The deal was cancelled.";
					mes "Come again next time.";
					close;
				}
				else if (.@input < 1 || .@input > 2000) {
					mes "[Gever Al Sharp]";
					mes "The number must";
					mes "be less than 2000.";
					next;
				}
				else {
					break;
				}
			}
			if (RENEWAL) {
				.@sell = .@input * 250;
			} else {
				.@sell = .@input * 8;
			}
			.@item_weight = .@input * 10;
			if (Zeny < .@sell) {
				mes "[Gever Al Sharp]";
				mes "You don't";
				mes "have enough zeny.";
				mes "Check how much zeny";
				mes "you have first.";
				close;
			}
			if ((MaxWeight - Weight) < .@item_weight) {
				mes "[Gever Al Sharp]";
				mes "It doesn't seem like";
				mes "you can carry everything.";
				mes "Please check the space";
				mes "in your inventory.";
				close;
			}
			Zeny -= .@sell;
			getitem Medicine_Bowl,.@input;
			mes "[Gever Al Sharp]";
			mes "Thank you.";
			mes "Come again.";
			close;
		}
		mes "[Gever Al Sharp]";
		mes "Well then,";
		mes "come again when";
		mes "you need to purchase";
		mes "materials related to";
		mes "Alchemy, alright?";
		close;
	case 2:
		mes "[Gever Al Sharp]";
		mes "What do you need?";
		mes "Manuals are generally 100,000 zeny. But there are a couple of special manuals that will cost more.";
		next;
		switch (select("Potion Creation Guide", "Alcohol Creation Guide", "Bottle Grenade Creation Guide", "Acid Bottle Creation Guide", "Plant Bottle Creation Guide", "Marine Sphere Bottle Creation Guide", "Glistening Coat Creation Guide", "Condensed Potion Creation Guide", "Cancel Deal.")) {
		case 1:
			callsub S_SellManual, Normal_Potion_Book, 100000;
			break;
		case 2:
			callsub S_SellManual, Alcol_Create_Book, 100000;
			break;
		case 3:
			callsub S_SellManual, FireBottle_Create_Book, 100000;
			break;
		case 4:
			callsub S_SellManual, Acid_Create_Book, 100000;
			break;
		case 5:
			callsub S_SellManual, Plant_Create_Book, 100000;
			break;
		case 6:
			callsub S_SellManual, Mine_Create_Book, 100000;
			break;
		case 7:
			callsub S_SellManual, Coating_Create_Book, 100000;
			break;
		case 8:
			callsub S_SellManual, Slim_Potion_Create_Book, 240000;
			break;
		case 9:
			mes "[Gever Al Sharp]";
			mes "Well then...";
			mes "Come back if you";
			mes "ever need to buy";
			mes "a production manual.";
			close;
		}
	case 3:
		mes "[Gever Al Sharp]";
		mes "Alright then,";
		mes "have a good day.";
		close;
	}

S_SellManual:
	if (Zeny < getarg(1)) {
		mes "[Gever Al Sharp]";
		mes "You don't";
		mes "have enough zeny.";
		mes "Check how much zeny";
		mes "you have first.";
		close;
	}
	mes "[Gever Al Sharp]";
	mes ""+ getitemname(getarg(0)) +"?";
	mes "That'll be "+ getarg(1) +" zeny.";
	next;
	if (select("Purchase.", "Quit.") == 1) {
		Zeny -= getarg(1);
		getitem getarg(0),1;
		mes "[Gever Al Sharp]";
		mes "Thank you for";
		mes "your patronage.";
		close;
	}
	mes "[Gever Al Sharp]";
	mes "Come again";
	mes "next time.";
	close;
}


alde_alche,31,186,3	script	Craft Book Merchant#alde	4_M_ALCHE_D,{
	mes "[Craft Book Merchant]";
	mes "Welcome.";
	mes "I'm here to sell";
	mes "^0000FFspecial craft books^000000 for Geneticists.";
	next;
	mes "[Craft Book Merchant]";
	mes "Geneticists have some skills";
	mes "that require craft books to activate.";
	next;
	mes "[Craft Book Merchant]";
	mes "The more craft books you have,";
	mes "the more skills you can use.";
	next;
	mes "[Craft Book Merchant]";
	mes "Please understand that";
	mes "I specialize in the sale of these special books and";
	mes "I ^FF0000don't sell other items^000000.";
	next;
	mes "[Craft Book Merchant]";
	mes "Would you like to take a look at my craft books?";
	next;
	if(select("Sure.", "No.") == 2) {
		mes "[Craft Book Merchant]";
		mes "Thank you for visiting my shop.";
		mes "Please be safe on your travels.";
		close;
	}
	if (checkweight(Knife,1) == 0) {
		mes "[Craft Book Merchant]";
		mes "I'm sorry, but your inventory is almost full.";
		mes "Please empty your inventory first.";
		close;
	}
	if (MaxWeight - Weight < 2500) {
		mes "[Craft Book Merchant]";
		mes "Please empty your inventory first.";
		close;
	}
	mes "[Craft Book Merchant]";
	mes "Okay then, here's the list of available craft books.";
	next;
	switch(select("[Apple Bomb Craft Book] 100,000 zeny", "[Pineapple Bomb Craft Book] 100,000 zeny", "[Coconut Bomb Craft Book] 100,000 zeny", "[Melon Bomb Craft Book] 100,000 zeny", "[Banana Bomb Craft Book] 100,000 zeny", "[Plant Gene Cultivation Method] 100,000 zeny", "[Superior Potion Craft Manual] 100,000 zeny", "[Mix Cooking Book] 100,000 zeny", "[Health Improvement Research Book] 100,000 zeny", "[Vigor Drink Recipe] 100,000 zeny", "Close")){
	case 1: .@item = 6279; break;
	case 2: .@item = 6280; break;
	case 3: .@item = 6281; break;
	case 4: .@item = 6282; break;
	case 5: .@item = 6283; break;
	case 6: .@item = 6284; break;
	case 7: .@item = 6285; break;
	case 8: .@item = 11022; break;
	case 9: .@item = 11023; break;
	case 10: .@item = 11024; break;
	case 11:
		mes "[Craft Book Merchant]";
		mes "Thank you for your patronage.";
		mes "Please come again.";
		close;
	}
	mes "[Craft Book Merchant]";
	mes "^0000FF["+getitemname(.@item)+"] costs";
	mes "100,000 zeny.";
	mes "How many would you like to purchase?";
	next;
	input .@amount;
	if (.@amount == 0) {
		mes "[Craft Book Merchant]";
		mes "Would you like to see some different books?";
		close;
	}
	if (.@amount < 1 || .@amount > 99 ){
		mes "[Craft Book Merchant]";
		mes "You cannot purchase more than 100 at a time.";
		close;
	}
	mes "[Craft Book Merchant]";
	mes "You've entered "+.@amount+"x ^0000FF["+getitemname(.@item)+"]^000000 to be purchased.";
	mes "Would you like to continue?";
	next;
	if (select("Yes", "No") == 1) {
		.@total = .@amount * 100000;
		if (Zeny < .@total) {
			mes "[Craft Book Merchant]";
			mes "I'm sorry, but you don't have enough money.";
			close;
		}
		if (checkweight(.@item,.@amount) == 0){
			mes "[Craft Book Merchant]";
			mes "It doesn't seem like you can carry everything.";
			mes "Please check the space in your inventory.";
			close;
		}
		mes "[Craft Book Merchant]";
		mes "Thank you for your patronage.";
		Zeny -= .@total;
		getitem .@item,.@amount;
		close;
	}
	mes "[Craft Book Merchant]";
	mes "Please take your time";
	mes "before you make your decision.";
	close;
}
