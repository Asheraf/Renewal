//================== RagEmu Script ========================================
//=     ______            _____                
//=     | ___ \          |  ___|               
//=     | |_/ /__ _  __ _| |__ _ __ ___  _   _ 
//=     |    // _` |/ _` |  __| '_ ` _ \| | | |
//=     | |\ \ (_| | (_| | |__| | | | | | |_| |
//=     \_| \_\__,_|\__, \____/_| |_| |_|\__,_|
//=                  __/ |                     
//=                 |___/                      
//=
//================= License ===============================================
//= This file is part of RagEmu.
//= http://ragemu.org - https://github.com/RagEmu/Renewal
//=
//= Copyright (C) 2016  RagEmu Dev Team
//= Copyright (C)  Euphy
//= Copyright (C)  Heris
//= Copyright (C)  Ziu
//=
//= RagEmu is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= Old Glast Heim
//================= Description ===========================================
//= Discover the history of events that took place in the Glast Heim castle
//= and how it ended up in ruins.
//================= Current Version =======================================
//= 1.4
//=========================================================================
//===== Description: ========================================= 
//= Discover the history of events that took place in the
//= Glast Heim castle and how it ended up in ruins.
//===== Additional Comments: ================================= 
//= 1.0 First version. [Euphy]
//=     This is a custom version, and may contain bugs.
//= 1.1 Bug fixes; removed redundant OnInstanceInit scripts.
//= 1.2 Add NPC Hugin's Follower [exneval]
//=     NPC that give access to Glast Heim Nightmare Mode.
//= 1.3 Add some NPCs placeholder. [exneval]
//= 1.4 Update to its official text. [exneval]
//=     Support merchant, enchant, socket features.
//============================================================ 

1@gl_k	mapflag	src4instance
2@gl_k	mapflag	src4instance

glast_01,204,273,6	script	Hugin#ghinstance	4_M_SAGE_C,{
	if (BaseLevel < 130) {
		mes "[Hugin]";
		mes "Why don't you come back after becoming stronger?";
		mes "Maybe, level 130.";
		close;
	}
	if (ghinstance == 1) {
		mes "[Hugin]";
		mes "Umm? Did you see me at another place? I don't think so. What about this time?";
		if (questprogress(12322) == 1)
			erasequest 12322;
		next;
	} else {
		mes "[Hugin]";
		mes "A long time ago, this castle did not like this.";
		next;
		mes "[Hugin]";
		mes "Ah I'm sorry. I'm muttering in front of a stranger.";
		next;
		mes "[Hugin]";
		mes "My name is Hugin. I'm studying the dimensional gap between time and space.";
		next;
		select("There's something like that here?");
		mes "[Hugin]";
		mes "Have you ever wondered about history of Glast Heim?";
		next;
		mes "[Hugin]";
		mes "There was a King named Shumiche who was known as a tyrant in the history of the Rune-Midgarts Kingdom.";
		next;
		mes "[Hugin]";
		mes "Time will reveal the true answers if we look in the right places.";
		next;
		if (select("I don't care about history","That is interesting. Find anything?") == 1) {
			mes "[Hugin]";
			mes "Really? Hmm, please come back later when you are interested then.";
			close;
		}
		mes "[Hugin]";
		mes "Actually, dimensional time travel is possible but I'm so afraid to go there.";
		next;
		mes "[Hugin]";
		mes "Yes! It might be possible for you.";
		next;
		mes "[Hugin]";
		mes "Do you want to time travel?";
		next;
		if (select("No, thanks.", "Yes, of course I do!") == 1) {
			mes "[Hugin]";
			mes "Really? But, jumping gigawatts this is such a great opportunity.";
			close;
		}
		mes "[Hugin]";
		mes "I knew that you would understand what I said. Well, please tell what you gonna do.";
		setquest 12316;
		ghinstance = 1;
		next;
	}
	.@ghins_time = questprogress(12317, PLAYTIME);
	if (.@ghins_time == 0) {
		.@party_id = getcharid(1);
		.@p_name$ = getpartyname(.@party_id);
		.@md_name$ = "Old Glast Heim";
		if (!instance_check_party(.@party_id)) {
			mes "[Hugin]";
			mes "Why don't you make a party with more than 1 person and talk to me again?";
			close;
		}
		if (getcharid(0) == getpartyleader(.@party_id,2)) {
			if (questprogress(12316) == 1)
				erasequest 12316;
			if (questprogress(12318, HUNTING) == 0)
				setquest 12318;
			.@menu$ = "Generate Time Gap:Enter Old Glast Heim:Cancel";
		} else {
			if (questprogress(12316) == 1) {
				mes "[Hugin]";
				mes "Have we met before? No way. It's my first time seeing you. What do you want?";
				erasequest 12316;
			}
			if (questprogress(12318, HUNTING) == 0)
				setquest 12318;
			.@menu$ = ":Enter Old Glast Heim:Cancel";
		}
		switch(select(.@menu$)) {
		case 1:
			.@instance = instance_create(.@md_name$, .@party_id);
			if (.@instance < 0) {
				mes "Party Name: "+.@p_name$;
				mes "Party Leader: "+strcharinfo(0);
				mes "^0000ff"+.@md_name$+" ^000000- Reservation Failed!";
				close;
			}
			if (instance_attachmap("1@gl_k", .@instance) == "" || instance_attachmap("2@gl_k", .@instance) == "") {
				mes "^0000ff"+.@md_name$+"^000000 - Reservation Failed!";
				instance_destroy(.@instance);
				close;
			}
			instance_set_timeout 3600,300,.@instance;
			instance_init(.@instance);
			mes "[Hugin]";
			mes "The time gap was created. When you're ready, talk to me again.";
			close;
		case 2:
			if (has_instance("1@gl_k") == "") {
				mes "The memorial dungeon "+.@md_name$+" does not exist.";
				mes "The party leader did not generate the dungeon yet.";
				close;
			} else {
				mapannounce "glast_01", .@p_name$ + " party member " + strcharinfo(0) + " enters the Old Glast Heim",bc_map,"0x00ff99";
				setquest 12317;
				warp "1@gl_k", 150, 20;
				end;
			}
			break;
		case 3:
			close;
		}
	} else if (.@ghins_time == 1) {
		mes "[Hugin]";
		mes "Oh, my...";
		mes "You still have after-effects of time travel. You can't travel again with this condition.";
		next;
		mes "[Hugin]";
		mes "Staying healthy is important so please take a break and come back again later.";
		close;
	} else {
		mes "^0000ffAll trace of access to Old Glast Heim have been removed. Now you can talk with Hugin again.^000000";
		if (questprogress(12318, HUNTING) == 0)
			setquest 12318;
		erasequest 12317;
		close;
	}
}

//== Floor 1 ===============================================
1@gl_k,149,41,6	script	Varmundt#ghinstance1	4_M_BARMUND,{
	if (getcharid(0) == getpartyleader(getcharid(1),2)) {
		mes "Hey ^0000ffguys^000000, were you sent here to help me?";
		npctalk "Hey guys, were you sent here to help me?";
		cutin "gl_barmund1",2;
		next;
		select("Oh. Well, about that...");
		mes "["+strcharinfo(0)+"]";
		mes "Oh yeah, hahaha, we were told to meet someone called Varmundt.";
		unittalk getcharid(3),"Oh yeah, hahaha, we were told to meet someone called Varmundt.";
		next;
		mes "[Varmundt]";
		mes "We don't have time. We must tell Sir Heinrich about Himelmez's invasion.";
		npctalk "We don't have time. We must tell Sir Heinrich about Himelmez's invasion.";
		cutin "gl_barmund2",2;
		next;
		select("What Himelmez...");
		mes "["+strcharinfo(0)+"]";
		mes "Himelmez? Who the hell is she?";
		unittalk getcharid(3),"Himelmez? Who the hell is she?";
		next;
		mes "[Varmundt]";
		mes "Didn't anybody give you the basic informations?";
		npctalk "Didn't anybody give you the basic informations?";
		cutin "gl_barmund3",2;
		next;
		mes "[Varmundt]";
		mes "The Valkyrie of the dead. She's after the Ymir's Heart pieces hidden here.";
		npctalk "The Valkyrie of the dead. She's after the Ymir's Heart pieces hidden here";
		cutin "gl_barmund2",2;
		next;
		mes "[Varmundt]";
		mes "She's capable of destroying the whole castle for this purpose.";
		npctalk "She's capable of destroying the whole castle for this purpose.";
		next;
		mes "[Varmundt]";
		mes "Hurry up! Inform Sir Heinrich that Himelmez is coming. I will briefly explore the enchantments she made to this place!";
		npctalk "Hurry up! Inform Sir Heinrich that Himelmez is coming. I will briefly explore the enchantments she made to this place!";
		close2;
		cutin "gl_barmund2",255;
		donpcevent instance_npcname("Varmundt#ghinstance1")+"::OnDisable";
		end;
	} else {
		cutin "gl_barmund2",2;
		mes "[Varmundt]";
		mes "Where's your leader? I need his help.";
		close2;
		cutin "gl_barmund2",255;
		end;
	}
OnInstanceInit:
	donpcevent instance_npcname("Varmundt#ghinstance1")+"::OnEnable";
	end;
OnDisable:
	hideonnpc instance_npcname("Varmundt#ghinstance1");
	hideoffnpc instance_npcname("Heinrich#ghinstance1");
	hideoffnpc instance_npcname("Varmundt#ghinstance2");
	end;
OnEnable:
	hideoffnpc instance_npcname("Varmundt#ghinstance1");
	end;
}

1@gl_k,145,54,6	script	Khalitzburg Crusader#1	4_F_KHALITZBURG,{
	mes "["+strnpcinfo(1)+"]";
	switch((atoi(strnpcinfo(2)) + 1) / 2) {
		case 1: mes "Would I be recruited?"; break;
		case 2: mes "May I help you?"; break;
		case 3: mes "..."; break;
		case 4: mes "I do not like to chit-chat during work."; break;
		case 5: mes "The aura of the castle has changed, don't you think? Something like a presence seemed to appear a little while ago."; break;
		case 6: mes "I wonder what commandant Varmundt is doing. I heard people are running away..."; break;
		case 7: mes "Quiet please."; break;
		case 8: mes "A fuss seems to be near."; break;
		case 9: mes "A dream last night really bothered me. My mother used to say that these dreams may come true..."; break;
		case 10: mes "Is Varmundt going with you? The commandant is waiting for you."; break;
		case 11: mes "Soon it's time to change shifts. I wonder what happened to the other knights."; break;
	}
	close;
OnInstanceInit:
OnDisable:
	hideonnpc instance_npcname(strnpcinfo(0));
	end;
OnEnable:
	hideoffnpc instance_npcname(strnpcinfo(0));
	end;
}
1@gl_k,154,54,3	duplicate(Khalitzburg Crusader#1)	Khalitzburg Crusader#2	4_F_KHALITZBURG
1@gl_k,145,59,6	duplicate(Khalitzburg Crusader#1)	White Knight#3	4_WHITEKNIGHT
1@gl_k,154,59,3	duplicate(Khalitzburg Crusader#1)	White Knight#4	4_WHITEKNIGHT
1@gl_k,145,64,6	duplicate(Khalitzburg Crusader#1)	Khalitzburg Crusader#5	4_F_KHALITZBURG
1@gl_k,154,64,3	duplicate(Khalitzburg Crusader#1)	Khalitzburg Crusader#6	4_F_KHALITZBURG
1@gl_k,145,69,6	duplicate(Khalitzburg Crusader#1)	White Knight#7	4_WHITEKNIGHT
1@gl_k,154,69,3	duplicate(Khalitzburg Crusader#1)	White Knight#8	4_WHITEKNIGHT
1@gl_k,145,74,6	duplicate(Khalitzburg Crusader#1)	Khalitzburg Crusader#9	4_F_KHALITZBURG
1@gl_k,154,74,3	duplicate(Khalitzburg Crusader#1)	Khalitzburg Crusader#10	4_F_KHALITZBURG
1@gl_k,145,79,6	duplicate(Khalitzburg Crusader#1)	White Knight#11	4_WHITEKNIGHT
1@gl_k,154,79,3	duplicate(Khalitzburg Crusader#1)	White Knight#12	4_WHITEKNIGHT
1@gl_k,145,84,6	duplicate(Khalitzburg Crusader#1)	Khalitzburg Crusader#13	4_F_KHALITZBURG
1@gl_k,154,84,3	duplicate(Khalitzburg Crusader#1)	Khalitzburg Crusader#14	4_F_KHALITZBURG
1@gl_k,145,89,6	duplicate(Khalitzburg Crusader#1)	White Knight#15	4_WHITEKNIGHT
1@gl_k,154,89,3	duplicate(Khalitzburg Crusader#1)	White Knight#16	4_WHITEKNIGHT
1@gl_k,145,94,6	duplicate(Khalitzburg Crusader#1)	Khalitzburg Crusader#17	4_F_KHALITZBURG
1@gl_k,154,94,3	duplicate(Khalitzburg Crusader#1)	Khalitzburg Crusader#18	4_F_KHALITZBURG
1@gl_k,145,99,6	duplicate(Khalitzburg Crusader#1)	White Knight#19	4_WHITEKNIGHT
1@gl_k,154,99,3	duplicate(Khalitzburg Crusader#1)	White Knight#20	4_WHITEKNIGHT
1@gl_k,145,104,6	duplicate(Khalitzburg Crusader#1)	Khalitzburg Crusader#21	4_F_KHALITZBURG
1@gl_k,154,104,3	duplicate(Khalitzburg Crusader#1)	Khalitzburg Crusader#22	4_F_KHALITZBURG

1@gl_k,149,100,6	script	Heinrich#ghinstance1	4_M_HEINRICH,{
	if (getcharid(0) == getpartyleader(getcharid(1),2)) {
		cutin "gl_heinrich2",2;
		select("Sir. Heinrich. Varmundt...");
		mes "["+strcharinfo(0)+"]";
		mes "Sir. Heinrich. Do you know what is happening in the castle now?";
		unittalk getcharid(3),"Sir. Heinrich. Do you know what is happening in the castle now?";
		next;
		mes "[Heinrich]";
		mes "Aren't you the adventurer that came along with Varmundt?";
		npctalk "Aren't you the adventurer that came along with Varmundt?";
		next;
		mes "[Heinrich]";
		mes "What is it? Something wrong with the castle?";
		npctalk "What is it? Something wrong with the castle?";
		next;
		select("Himelmez's invasion...");
		mes "["+strcharinfo(0)+"]";
		mes "Dead man's Valkyrie, Himelmez is coming to take the Ymir's Heart piece hidden inside this castle!";
		unittalk getcharid(3),"Dead man's Valkyrie, Himelmez is coming to take the Ymir's Heart piece hidden inside this castle!";
		next;
		cutin "gl_heinrich1",2;
		mes "[Heinrich]";
		mes "Haha. Funny. Do you really think that is possible?";
		npctalk "Haha. Funny. Do you really think that is possible?";
		next;
		cutin "gl_barmund2",2;
		mes "[Varmundt]";
		mes "I'm not kidding, Sir. Heinrich. If I'm correct, then she will be here very soon.";
		donpcevent instance_npcname("Varmundt#ghinstance2")+"::OnTalk1";
		next;
		mes "[Varmundt]";
		mes "We must hide the heart piece to a safe place before Himelmez's attack starts!";
		donpcevent instance_npcname("Varmundt#ghinstance2")+"::OnTalk2";
		next;
		select("Even if you don't believe me...");
		mes "["+strcharinfo(0)+"]";
		mes "I can't make you believe me, but there's no time to argue!";
		unittalk getcharid(3),"I can't make you believe me, but there's no time to argue!";
		cutin "gl_barmund2",255;
		next;
		cutin "gl_heinrich1",2;
		mes "[Heinrich]";
		mes "Thank you for the help. But, we don't even have our king with us right now.";
		npctalk "Thank you for the help. But, we don't even have our king with us right now.";
		next;
		mes "[Heinrich]";
		mes "We cannot risk moving the heart just because some stranger says so.";
		npctalk "We cannot risk moving the heart just because some stranger says so.";
		close2;
		donpcevent instance_npcname("Heinrich#ghinstance1")+"::OnDisable";
		donpcevent instance_npcname("Heinrich#ghinstance2")+"::OnEnable";
		donpcevent instance_npcname("Himelmez#ghinstance1")+"::OnEnable";
		donpcevent instance_npcname("#talkinstance1")+"::OnEnable";
		mapannounce instance_mapname("1@gl_k"),"????'s: Muahahahaha~!",bc_map,"0xFFFF00",FW_NORMAL,18;
		cutin "gl_heinrich1",255;
		end;
	} else {
		cutin "gl_heinrich2",2;
		mes "[Heinrich]";
		mes "Where is your leader? I must talk to him.";
		close2;
		cutin "gl_heinrich2",255;
		end;
	}
OnInstanceInit:
OnDisable:
	hideonnpc instance_npcname("Heinrich#ghinstance1");
	end;
OnEnable:
	hideoffnpc instance_npcname("Heinrich#ghinstance1");
	end;
}

1@gl_k,152,97,3	script	Varmundt#ghinstance2	4_M_BARMUND,{
	end;
OnInstanceInit:
OnDisable:
	hideonnpc instance_npcname("Varmundt#ghinstance2");
	end;
OnEnable:
	hideoffnpc instance_npcname("Varmundt#ghinstance2");
	end;
OnTalk1:
	npctalk "I'm not kidding, Sir. Heinrich. If I'm correct, then she will be here very soon.";
	end;
OnTalk2:
	npctalk "We must hide the heart piece to a safe place before Himelmez's attack starts!";
	end;
OnTalk3:
	npctalk "Himelmez!! Already!";
	end;
OnTalk4:
	npctalk "Sir. Heinrich, they're all monsters. You need to give them rest!";
	end;
OnTalk5:
	npctalk "Sir. Heinrich! We don't have much time!";
	end;
OnTalk6:
	npctalk "If you hurry now, there's a chance!";
	end;
}

1@gl_k,149,97,6	script	Heinrich#ghinstance2	4_M_HEINRICH,{
	end;
OnInstanceInit:
OnDisable:
	hideonnpc instance_npcname("Heinrich#ghinstance2");
	end;
OnEnable:
	hideoffnpc instance_npcname("Heinrich#ghinstance2");
	end;
OnTalk1:
	npctalk "Who are you?";
	end;
OnTalk2:
	npctalk "Aren't you a little too feminine to be the Ruler of death? We are not afraid of you...";
	end;
OnTalk3:
	npctalk "What?";
	end;
OnTalk4:
	npctalk "My king is visiting the Rune-Midgarts royal family. He's not back yet.";
	end;
OnTalk5:
	npctalk "And now you are trying to trick me, what has happened to him?!";
	end;
OnTalk6:
	npctalk "She probably already knows where the Ymir's heart piece is.";
	end;
OnTalk7:
	npctalk "All Khalitzburg and White Knights should follow me now...";
	end;
OnTalk8:
	npctalk "My men... This can't be happening!";
	end;
OnTalk9:
	npctalk "I'm so sorry...";
	end;
OnTalk10:
	npctalk "I am sorry, My Lord! Forgive me!";
	end;
}

1@gl_k,149,100,6	script	Heinrich#ghinstance3	4_M_HEINRICH,{
	end;
OnInstanceInit:
OnDisable:
	hideonnpc instance_npcname("Heinrich#ghinstance3");
	end;
OnEnable:
	hideoffnpc instance_npcname("Heinrich#ghinstance3");
	end;
OnTalk1:
	npctalk "I killed my own men...";
	end;
OnTalk2:
	npctalk "Varmundt is right. Now is not the time for mourning.";
	end;
OnTalk3:
	npctalk "I have a request to you followers.";
	end;
OnTalk4:
	npctalk "Himelmez can turn living beings into monsters.";
	end;
OnTalk5:
	npctalk "But, there might still be survivors here.";
	end;
OnTalk6:
	npctalk "Destroy the monsters and find any survivors.";
	end;
OnTalk7:
	npctalk "Varmundt and I will chase Himelmez.";
	end;
OnTalk8:
	npctalk "Very well, Varmundt. Let's find Himelmez.";
	end;
}

1@gl_k,149,89,1	script	Himelmez#ghinstance1	4_F_HIMEL,{
	end;
OnInstanceInit:
OnDisable:
	hideonnpc instance_npcname("Himelmez#ghinstance1");
	end;
OnEnable:
	hideoffnpc instance_npcname("Himelmez#ghinstance1");
	end;
OnTalk1:
	npctalk "Well well~ Am I interrupting you? Weren't you expecting me?";
	end;
OnTalk2:
	npctalk "My name is Lisa Kahn Himelmez. Master of Dullahan, Dead man's Valkyrie, that's what they call me.";
	end;
OnTalk3:
	npctalk "Let's see if you can relax like that after you find out where your king is.";
	end;
OnTalk4:
	npctalk "Hmm, now I have your attention do I not?";
	end;
OnTalk5:
	npctalk "It's a pity to meet you in a situation like this.";
	end;
OnTalk6:
	npctalk "Well~ I would love to sit down and explain for you, but I'm kind of busy today~";
	end;
OnTalk7:
	npctalk "I have business to take care of. My minions will treat you well enough for me~";
	end;
}

//== Control Timer =========================================
1@gl_k,0,0,0	script	#talkinstance1	FAKE_NPC,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#talkinstance1");
	end;
OnEnable:
	enablenpc instance_npcname("#talkinstance1");
	initnpctimer;
	end;
OnTimer1500:
	donpcevent instance_npcname("Heinrich#ghinstance2")+"::OnTalk1";
	end;
OnTimer4500:
	donpcevent instance_npcname("Himelmez#ghinstance1")+"::OnTalk1";
	end;
OnTimer10000:
	donpcevent instance_npcname("Varmundt#ghinstance2")+"::OnTalk3";
	end;
OnTimer17500:
	donpcevent instance_npcname("Himelmez#ghinstance1")+"::OnTalk2";
	end;
OnTimer22000:
	donpcevent instance_npcname("Heinrich#ghinstance2")+"::OnTalk2";
	end;
OnTimer28500:
	donpcevent instance_npcname("Himelmez#ghinstance1")+"::OnTalk3";
	end;
OnTimer36000:
	donpcevent instance_npcname("Heinrich#ghinstance2")+"::OnTalk3";
	end;
OnTimer41000:
	donpcevent instance_npcname("Himelmez#ghinstance1")+"::OnTalk4";
	end;	
OnTimer54000:
	donpcevent instance_npcname("Himelmez#ghinstance1")+"::OnTalk5";
	end;
OnTimer59000:
	donpcevent instance_npcname("Heinrich#ghinstance2")+"::OnTalk4";
	end;
OnTimer66500:
	donpcevent instance_npcname("Heinrich#ghinstance2")+"::OnTalk5";
	end;
OnTimer71500:
	donpcevent instance_npcname("Himelmez#ghinstance1")+"::OnTalk6";
	end;
OnTimer78000:
	donpcevent instance_npcname("Himelmez#ghinstance1")+"::OnTalk7";
	end;
OnTimer84500:
	donpcevent instance_npcname("Heinrich#ghinstance2")+"::OnTalk6";
	donpcevent instance_npcname("Himelmez#ghinstance1")+"::OnDisable";
	end;
OnTimer90500:
	donpcevent instance_npcname("Heinrich#ghinstance2")+"::OnTalk7";
	end;
OnTimer92000:
	for(.@i = 1; .@i<=20; .@i += 4) {
		hideonnpc instance_npcname("Khalitzburg Crusader#"+.@i);
		hideonnpc instance_npcname("Khalitzburg Crusader#"+(.@i+1));
		hideonnpc instance_npcname("White Knight#"+(.@i+2));
		hideonnpc instance_npcname("White Knight#"+(.@i+3));
	}
	hideonnpc instance_npcname("Khalitzburg Crusader#21");
	hideonnpc instance_npcname("Khalitzburg Crusader#22");

	.@map$ = instance_mapname("1@gl_k");
	.@label$ = instance_npcname("#talkinstance1")+"::OnMyMobDead";
	monster .@map$,145,59,"Abysmal Knight",MG_KNIGHT_OF_ABYSS,1,.@label$;
	monster .@map$,154,59,"Abysmal Knight",MG_KNIGHT_OF_ABYSS,1,.@label$;
	monster .@map$,145,69,"Abysmal Knight",MG_KNIGHT_OF_ABYSS,1,.@label$;
	monster .@map$,154,69,"Abysmal Knight",MG_KNIGHT_OF_ABYSS,1,.@label$;
	monster .@map$,145,79,"Abysmal Knight",MG_KNIGHT_OF_ABYSS,1,.@label$;
	monster .@map$,154,79,"Abysmal Knight",MG_KNIGHT_OF_ABYSS,1,.@label$;
	monster .@map$,145,89,"Abysmal Knight",MG_KNIGHT_OF_ABYSS,1,.@label$;
	monster .@map$,154,89,"Abysmal Knight",MG_KNIGHT_OF_ABYSS,1,.@label$;
	monster .@map$,145,99,"Abysmal Knight",MG_KNIGHT_OF_ABYSS,1,.@label$;
	monster .@map$,154,99,"Abysmal Knight",MG_KNIGHT_OF_ABYSS,1,.@label$;

	.@mob_id = monster(.@map$,145,54,"Khalitzburg",MG_KHALITZBURG,1,.@label$);
	unittalk .@mob_id, "Water... Someone give me water...";
	.@mob_id = monster(.@map$,154,54,"Khalitzburg",MG_KHALITZBURG,1,.@label$);
	unittalk .@mob_id, "Kkkrrrruughgh...";
	.@mob_id = monster(.@map$,145,64,"Khalitzburg",MG_KHALITZBURG,1,.@label$);
	unittalk .@mob_id, "Sir. Heinrich. Save me...";
	.@mob_id = monster(.@map$,154,64,"Khalitzburg",MG_KHALITZBURG,1,.@label$);
	unittalk .@mob_id, "I miss my sister...";
	.@mob_id = monster(.@map$,145,74,"Khalitzburg",MG_KHALITZBURG,1,.@label$);
	unittalk .@mob_id, "Don't leave me alone, help me.";
	.@mob_id = monster(.@map$,154,74,"Khalitzburg",MG_KHALITZBURG,1,.@label$);
	unittalk .@mob_id, "Aaarrrrrhhhh";
	.@mob_id = monster(.@map$,145,84,"Khalitzburg",MG_KHALITZBURG,1,.@label$);
	unittalk .@mob_id, "I am so thirsty.";
	.@mob_id = monster(.@map$,154,84,"Khalitzburg",MG_KHALITZBURG,1,.@label$);
	unittalk .@mob_id, "Oh...No...I can't die yet...";
	.@mob_id = monster(.@map$,145,94,"Khalitzburg",MG_KHALITZBURG,1,.@label$);
	unittalk .@mob_id, "I feel sick to my stomach urrgg";
	.@mob_id = monster(.@map$,154,94,"Khalitzburg",MG_KHALITZBURG,1,.@label$);
	unittalk .@mob_id, "Can someone...";
	.@mob_id = monster(.@map$,145,104,"Khalitzburg",MG_KHALITZBURG,1,.@label$);
	unittalk .@mob_id, "Arrgg... My body";
	.@mob_id = monster(.@map$,154,104,"Khalitzburg",MG_KHALITZBURG,1,.@label$);
	unittalk .@mob_id, "My throat is burning!";
	end;
OnTimer94000:
	donpcevent instance_npcname("Heinrich#ghinstance2")+"::OnTalk8";
	end;
OnTimer96500:
	donpcevent instance_npcname("Varmundt#ghinstance2")+"::OnTalk4";
	end;
OnTimer100000:
	donpcevent instance_npcname("Heinrich#ghinstance2")+"::OnTalk9";
	end;
OnTimer103000:
	donpcevent instance_npcname("Heinrich#ghinstance2")+"::OnTalk10";
	end;
OnTimer106000:
	mapannounce instance_mapname("1@gl_k"),"Sir. Heinrich: Death to all!",bc_map,"0xFFFF00",FW_NORMAL,18;
	end;
OnTimer109000:
	mapannounce instance_mapname("1@gl_k"),"Sir. Heinrich: Go back to the darkness!",bc_map,"0xFFFF00",FW_NORMAL,18;
	end;
OnTimer109500:
	enablenpc instance_npcname(".#ghinstance22");
	enablenpc instance_npcname(".#ghinstance21");
	donpcevent instance_npcname(".#ghinstance22")+"::OnEffect1";
	donpcevent instance_npcname(".#ghinstance21")+"::OnEffect1";
	end;
OnTimer110000:
	enablenpc instance_npcname(".#ghinstance20");
	enablenpc instance_npcname(".#ghinstance19");
	enablenpc instance_npcname(".#ghinstance18");
	enablenpc instance_npcname(".#ghinstance17");
	donpcevent instance_npcname(".#ghinstance20")+"::OnEffect1";
	donpcevent instance_npcname(".#ghinstance19")+"::OnEffect1";
	donpcevent instance_npcname(".#ghinstance18")+"::OnEffect1";
	donpcevent instance_npcname(".#ghinstance17")+"::OnEffect1";
	end;
OnTimer110500:
	enablenpc instance_npcname(".#ghinstance16");
	enablenpc instance_npcname(".#ghinstance15");
	enablenpc instance_npcname(".#ghinstance14");
	enablenpc instance_npcname(".#ghinstance13");
	donpcevent instance_npcname(".#ghinstance16")+"::OnEffect1";
	donpcevent instance_npcname(".#ghinstance15")+"::OnEffect1";
	donpcevent instance_npcname(".#ghinstance14")+"::OnEffect1";
	donpcevent instance_npcname(".#ghinstance13")+"::OnEffect1";
	end;
OnTimer111000:
	enablenpc instance_npcname(".#ghinstance12");
	enablenpc instance_npcname(".#ghinstance11");
	enablenpc instance_npcname(".#ghinstance10");
	enablenpc instance_npcname(".#ghinstance9");
	donpcevent instance_npcname(".#ghinstance12")+"::OnEffect1";
	donpcevent instance_npcname(".#ghinstance11")+"::OnEffect1";
	donpcevent instance_npcname(".#ghinstance10")+"::OnEffect1";
	donpcevent instance_npcname(".#ghinstance9")+"::OnEffect1";
	end;
OnTimer111500:
	enablenpc instance_npcname(".#ghinstance8");
	enablenpc instance_npcname(".#ghinstance7");
	enablenpc instance_npcname(".#ghinstance6");
	enablenpc instance_npcname(".#ghinstance5");
	donpcevent instance_npcname(".#ghinstance8")+"::OnEffect1";
	donpcevent instance_npcname(".#ghinstance7")+"::OnEffect1";
	donpcevent instance_npcname(".#ghinstance6")+"::OnEffect1";
	donpcevent instance_npcname(".#ghinstance5")+"::OnEffect1";
	end;
OnTimer112000:
	enablenpc instance_npcname(".#ghinstance4");
	enablenpc instance_npcname(".#ghinstance3");
	enablenpc instance_npcname(".#ghinstance2");
	enablenpc instance_npcname(".#ghinstance1");
	donpcevent instance_npcname(".#ghinstance4")+"::OnEffect1";
	donpcevent instance_npcname(".#ghinstance3")+"::OnEffect1";
	donpcevent instance_npcname(".#ghinstance2")+"::OnEffect1";
	donpcevent instance_npcname(".#ghinstance1")+"::OnEffect1";
	donpcevent instance_npcname("Heinrich#ghinstance3")+"::OnEnable";
	donpcevent instance_npcname("Heinrich#ghinstance2")+"::OnDisable";

	for (.@i = 1; .@i <= 22; ++.@i)
		disablenpc instance_npcname(".#ghinstance"+ .@i);

	killmonster instance_mapname("1@gl_k"), instance_npcname("#talkinstance1")+"::OnMyMobDead";
	end;
OnTimer112500:
	donpcevent instance_npcname("Heinrich#ghinstance3")+"::OnTalk1";
	end;
OnTimer115500:
	donpcevent instance_npcname("Varmundt#ghinstance2")+"::OnTalk5";
	end;
OnTimer118500:
	donpcevent instance_npcname("Varmundt#ghinstance2")+"::OnTalk6";
	end;
OnTimer121500:
	donpcevent instance_npcname("Heinrich#ghinstance3")+"::OnTalk2";
	end;
OnTimer124500:
	donpcevent instance_npcname("Heinrich#ghinstance3")+"::OnTalk3";
	end;
OnTimer127500:
	donpcevent instance_npcname("Heinrich#ghinstance3")+"::OnTalk4";
	end;
OnTimer130500:
	donpcevent instance_npcname("Heinrich#ghinstance3")+"::OnTalk5";
	end;
OnTimer134500:
	donpcevent instance_npcname("Heinrich#ghinstance3")+"::OnTalk6";
	end;
OnTimer138500:
	donpcevent instance_npcname("Heinrich#ghinstance3")+"::OnTalk7";
	end;
OnTimer143500:
	donpcevent instance_npcname("Heinrich#ghinstance3")+"::OnTalk8";
	end;
OnTimer147500:
	donpcevent instance_npcname("Heinrich#ghinstance3")+"::OnDisable";
	end;
OnTimer148500:
	donpcevent instance_npcname("Varmundt#ghinstance2")+"::OnDisable";
	stopnpctimer;
	donpcevent instance_npcname("#ghinstancewarp1")+"::OnEnable";
	donpcevent instance_npcname("#ghinstancewarp2")+"::OnEnable";
	mapannounce instance_mapname("1@gl_k"),"A portal has opened to the west.", bc_map, "0xFFFF00";
	donpcevent instance_npcname("#talkinstance1")+"::OnDisable";
	donpcevent instance_npcname("#ghmemorialmob01")+"::OnEnable";
	end;
OnMyMobDead:
	end;
}

1@gl_k,145,54,6	script	.#ghinstance1	HIDDEN_NPC,{
	end;
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnEnable:
	enablenpc instance_npcname(strnpcinfo(0));
	end;
OnEffect1:
	specialeffect EF_GRANDCROSS;
	specialeffect EF_LEXAETERNA;
	end;
}

1@gl_k,154,54,3	duplicate(.#ghinstance1)	.#ghinstance2	HIDDEN_NPC
1@gl_k,145,59,6	duplicate(.#ghinstance1)	.#ghinstance3	HIDDEN_NPC
1@gl_k,154,59,3	duplicate(.#ghinstance1)	.#ghinstance4	HIDDEN_NPC
1@gl_k,145,64,6	duplicate(.#ghinstance1)	.#ghinstance5	HIDDEN_NPC
1@gl_k,154,64,3	duplicate(.#ghinstance1)	.#ghinstance6	HIDDEN_NPC
1@gl_k,145,69,6	duplicate(.#ghinstance1)	.#ghinstance7	HIDDEN_NPC
1@gl_k,154,69,3	duplicate(.#ghinstance1)	.#ghinstance8	HIDDEN_NPC
1@gl_k,145,74,6	duplicate(.#ghinstance1)	.#ghinstance9	HIDDEN_NPC
1@gl_k,154,74,3	duplicate(.#ghinstance1)	.#ghinstance10	HIDDEN_NPC
1@gl_k,145,79,6	duplicate(.#ghinstance1)	.#ghinstance11	HIDDEN_NPC
1@gl_k,154,79,3	duplicate(.#ghinstance1)	.#ghinstance12	HIDDEN_NPC
1@gl_k,145,84,6	duplicate(.#ghinstance1)	.#ghinstance13	HIDDEN_NPC
1@gl_k,154,84,3	duplicate(.#ghinstance1)	.#ghinstance14	HIDDEN_NPC
1@gl_k,145,89,6	duplicate(.#ghinstance1)	.#ghinstance15	HIDDEN_NPC
1@gl_k,154,89,3	duplicate(.#ghinstance1)	.#ghinstance16	HIDDEN_NPC
1@gl_k,145,94,6	duplicate(.#ghinstance1)	.#ghinstance17	HIDDEN_NPC
1@gl_k,154,94,3	duplicate(.#ghinstance1)	.#ghinstance18	HIDDEN_NPC
1@gl_k,145,99,6	duplicate(.#ghinstance1)	.#ghinstance19	HIDDEN_NPC
1@gl_k,154,99,3	duplicate(.#ghinstance1)	.#ghinstance20	HIDDEN_NPC
1@gl_k,145,104,6	duplicate(.#ghinstance1)	.#ghinstance21	HIDDEN_NPC
1@gl_k,154,104,3	duplicate(.#ghinstance1)	.#ghinstance22	HIDDEN_NPC

1@gl_k,96,80,0	script	#ghinstancewarp1	WARPNPC,1,2,{
	end;
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnEnable:
	enablenpc instance_npcname(strnpcinfo(0));
	end;
OnTouch:
	.@map1$ = instance_mapname("1@gl_k");
	.@map2$ = instance_mapname("2@gl_k");
	switch(atoi(replacestr(strnpcinfo(2),"ghinstancewarp",""))) {
		case 1: warp .@map1$,80,80; break;
		case 2: warp .@map1$,105,80; break;
		case 3: warp .@map1$,215,79; break;
		case 4: warp .@map1$,195,79; break;
		case 5: warp .@map1$,215,216; break;
		case 6: warp .@map1$,235,216; break;
		case 7: warp .@map2$,150,46; break;
		case 8: warp .@map2$,126,123; break;
		case 9: warp .@map2$,150,116; break;
		case 10: warp .@map2$,174,101; break;
		case 11: warp .@map2$,150,110; break;
		case 12: warp .@map2$,150,179; break;
		case 13: warp .@map2$,150,160; break;
		case 14: warp .@map1$,150,281; break;
		case 15: warp .@map1$,48,168; break;
	}
	end;
}
1@gl_k,90,80,0	duplicate(#ghinstancewarp1)	#ghinstancewarp2	WARPNPC,2,2
1@gl_k,202,79,0	duplicate(#ghinstancewarp1)	#ghinstancewarp3	WARPNPC,2,2
1@gl_k,206,79,0	duplicate(#ghinstancewarp1)	#ghinstancewarp4	WARPNPC,2,2
1@gl_k,228,216,0	duplicate(#ghinstancewarp1)	#ghinstancewarp5	WARPNPC,2,2
1@gl_k,222,216,0	duplicate(#ghinstancewarp1)	#ghinstancewarp6	WARPNPC,2,2
1@gl_k,150,284,0	duplicate(#ghinstancewarp1)	#ghinstancewarp7	WARPNPC,2,2
2@gl_k,145,123,0	duplicate(#ghinstancewarp1)	#ghinstancewarp8	WARPNPC,2,2
2@gl_k,136,122,0	duplicate(#ghinstancewarp1)	#ghinstancewarp9	WARPNPC,2,2
2@gl_k,154,101,0	duplicate(#ghinstancewarp1)	#ghinstancewarp10	WARPNPC,2,2
2@gl_k,165,101,0	duplicate(#ghinstancewarp1)	#ghinstancewarp11	WARPNPC,2,2
2@gl_k,150,163,0	duplicate(#ghinstancewarp1)	#ghinstancewarp12	WARPNPC,2,2
2@gl_k,150,167,0	duplicate(#ghinstancewarp1)	#ghinstancewarp13	WARPNPC,2,2
2@gl_k,150,32,0	duplicate(#ghinstancewarp1)	#ghinstancewarp14	WARPNPC,2,2
1@gl_k,69,168,0	duplicate(#ghinstancewarp1)	#ghinstancewarp15	WARPNPC,2,2

//== Sector 1 Mobs =========================================
1@gl_k,0,0,0	script	#ghmemorialmob01	FAKE_NPC,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#ghmemorialmob01");
	end;
OnEnable:
	enablenpc instance_npcname("#ghmemorialmob01");
	.@map$ = instance_mapname("1@gl_k");
	.@label$ = instance_npcname("#ghmemorialmob01")+"::OnMyMobDead";
	areamonster .@map$,76,99,87,10,"Suffering Chamberlain",2466,15,.@label$;
	areamonster .@map$,67,39,12,6,"Fallen Monk",2465,20,.@label$;
	areamonster .@map$,67,39,12,6,"Suffering Chamberlain",2466,3,.@label$;
	areamonster .@map$,32,75,51,58,"Decayed Butler",2464,6,.@label$;
	areamonster .@map$,45,84,6,137,"Decayed Butler",2464,12,.@label$;
	end;
OnMyMobDead:
	.@map$ = instance_mapname("1@gl_k");
	.@label$ = instance_npcname("#ghmemorialmob01")+"::OnMyMobDead";
	.@mob_dead_num = 56 - mobcount(.@map$,.@label$);
	if (.@mob_dead_num > 35) {
		mapannounce .@map$, "Seems like Himelmez drains the dead's souls. They must be destroyed.",bc_map,"0xFFFFFF";
		killmonster .@map$,.@label$;
		donpcevent instance_npcname("Aspiring Butcher#clearGH")+"::OnEnable";
		donpcevent instance_npcname("#ghmemorialmob01")+"::OnDisable";
	}
	end;
}

1@gl_k,17,51,3	script	Aspiring Butcher#clearGH	4_M_KID1,{
	if (getcharid(0) == getpartyleader(getcharid(1),2)) {
		mes "[Aspiring Butcher]";
		mes "Help me! Help me!!!";
		next;
		select("Hey, wake up! Are there any other survivors?");
		mes "["+strcharinfo(0)+"]";
		mes "Hey, wake up! Are you alone?";
		unittalk getcharid(3),"Hey, wake up! Are you alone?";
		next;
		mes "[Aspiring Butcher]";
		mes "The Chamberlain... the Monk... They've become monsters. I couldn't do anything.";
		npctalk "The Chamberlain... the Monk... They've become monsters. I couldn't do anything.";
		next;
		mes "[Aspiring Butcher]";
		mes "I just stood still... Nothing, I couldn't do anything...";
		npctalk "I just stood still... Nothing, I couldn't do anything...";
		next;
		select("Wake up!");
		mes "["+strcharinfo(0)+"]";
		mes "Wake up kid! Go east along the central passage to the outside! The path is safe!";
		unittalk getcharid(3),"Wake up kid! Go east along the central passage to the outside! The path is safe!";
		next;
		mes "[Aspiring Butcher]";
		mes "East passage? Alone? How?";
		npctalk "East passage? Alone? How?";
		next;
		select("I can guide you through the path.");
		mes "["+strcharinfo(0)+"]";
		mes "I will guide you, perhaps that will help. If you want to close your eyes try not to hit anything.";
		unittalk getcharid(3),"I will guide you, perhaps that will help. If you want to close your eyes try not to hit anything.";
		next;
		mes "[Aspiring Butcher]";
		mes "Aspiring Butcher: I know, I... I'm trying to.";
		npctalk "Aspiring Butcher: I know, I... I'm trying to.";
		donpcevent instance_npcname("Aspiring Butcher#clearGH")+"::OnDisable";
		donpcevent instance_npcname("#ghmemorialmob02")+"::OnEnable";
		close;
	} else {
		mes "[Aspiring Butcher]";
		mes "Somebody help me, somebody help me, please.";
		close;
	}
OnInstanceInit:
OnDisable:
	hideonnpc instance_npcname("Aspiring Butcher#clearGH");
	end;
OnEnable:
	hideoffnpc instance_npcname("Aspiring Butcher#clearGH");
	end;
}

//== Sector 2 Mobs =========================================
1@gl_k,291,145,3	script	Hollgrehenn Destroyer	4_F_JOB_BLACKSMITH,{
	if (getcharid(0) == getpartyleader(getcharid(1),2)) {
		mes "[Hollgrehenn Destroyer]";
		mes "Yaaa!! Die!!!";
		npctalk "Yaaa!! Die!!!";
		specialeffect EF_CRASHEARTH;
		next;
		select("Don't worry!");
		mes "["+strcharinfo(0)+"]";
		mes "Don't worry! Mam. Are you alone? No other survivors?";
		unittalk getcharid(3),"Don't worry! Mam. Are you alone? No other survivors?";
		next;
		mes "[Hollgrehenn Destroyer]";
		mes "I'm the only survivor left";
		npctalk "I'm the only survivor left";
		next;
		select("This is a very dangerous place.");
		mes "["+strcharinfo(0)+"]";
		mes "This is a very dangerous place. You know the central passage? Do you think you can move... and get to a safer place?";
		unittalk getcharid(3),"This is a very dangerous place. You know the central passage? Do you think you can move... and get to a safer place?";
		next;
		mes "[Hollgrehenn Destroyer]";
		mes "Yes, I am able to move. I'll move for my baby's sake.";
		npctalk "Yes, I am able to move. I'll move for my baby's sake.";
		next;
		select("Survive the road...");
		mes "["+strcharinfo(0)+"]";
		mes "You and your baby will get out of here safely. But I'm sorry I can't help you more.";
		unittalk getcharid(3),"You and your baby will get out of here safely. But I'm sorry I can't help you more.";
		next;
		mes "[Hollgrehenn Destroyer]";
		mes "That's ok. Thank you for helping us. I'm good to go alone. Ah and good luck also.";
		npctalk "That's ok. Thank you for helping us. I'm good to go alone. Ah and good luck also.";
		donpcevent instance_npcname("Hollgrehenn Destroyer")+"::OnDisable";
		donpcevent instance_npcname("#ghmemorialmob03")+"::OnEnable";
		close;
	} else {
		mes "[Hollgrehenn Destroyer]";
		mes "Somebody help me, somebody help me, please";
		close;
	}
OnInstanceInit:
OnDisable:
	hideonnpc instance_npcname("Hollgrehenn Destroyer");
	end;
OnEnable:
	hideoffnpc instance_npcname("Hollgrehenn Destroyer");
	end;
}

1@gl_k,0,0,0	script	#ghmemorialmob02	FAKE_NPC,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#ghmemorialmob02");
	end;
OnEnable:
	enablenpc instance_npcname("#ghmemorialmob02");
	donpcevent instance_npcname("#ghinstancewarp3")+"::OnEnable";
	donpcevent instance_npcname("#ghinstancewarp4")+"::OnEnable";
	.@map$ = instance_mapname("1@gl_k");
	.@label$ = instance_npcname("#ghmemorialmob02")+"::OnMyMobDead";
	mapannounce .@map$,"A portal has opened to the east.",bc_map,"0xFFFF00";
	areamonster .@map$,241,113,291,19,"Outraged Refiner",2466,12,.@label$;
	areamonster .@map$,241,113,291,19,"Decomposed Blacksmith",2464,12,.@label$;
	areamonster .@map$,227,217,291,135,"Outraged Refiner",2466,12,.@label$;
	areamonster .@map$,227,217,291,135,"Decomposed Blacksmith",2464,12,.@label$;
	end;
OnMyMobDead:
	.@map$ = instance_mapname("1@gl_k");
	.@label$ = instance_npcname("#ghmemorialmob02")+"::OnMyMobDead";
	.@mob_dead_num = 48 - mobcount(.@map$,.@label$);
	if (.@mob_dead_num > 28) {
		mapannounce .@map$,"Himelmez's curse is getting weaker. Find any survivors!",bc_map,"0xFFFFFF";
		killmonster .@map$,.@label$;
		donpcevent instance_npcname("Hollgrehenn Destroyer")+"::OnEnable";
		donpcevent instance_npcname("#ghmemorialmob02")+"::OnDisable";
	}
	end;
}

//== Tramp Mobs ============================================
1@gl_k,221,82,3	script	Breathless Man#GHtramp1	4_M_DIEMAN,4,4,{
	end;
OnTouch:
	.@i = rand(1,10);
	if (.@i == 1) .@mobs = 3;
	else if (.@i == 2) .@mobs = 4;
	else if (.@i == 3) .@mobs = 5;
	else if (.@i < 7) .@mobs = 6;
	else .@mobs = 7;
	getmapxy(.@map$, .@x, .@y, UNITTYPE_NPC);
	specialeffect EF_VENOMDUST;
	monster .@map$,.@x,.@y,"Maggot",2467,.@mobs,instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnMyMobDead:
	end;
OnInstanceInit:
OnEnable:
	enablenpc instance_npcname(strnpcinfo(0));
	end;
OnDisable:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
}

1@gl_k,213,63,7	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp2	4_M_DIEMAN,4,4
1@gl_k,230,50,2	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp3	4_M_DIEMAN,4,4
1@gl_k,222,39,2	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp4	4_M_DIEMAN,4,4
1@gl_k,214,27,3	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp5	4_M_DIEMAN,4,4
1@gl_k,223,17,2	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp6	4_M_DIEMAN,4,4
1@gl_k,235,16,4	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp7	4_M_DIEMAN,4,4
1@gl_k,251,20,5	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp8	4_M_DIEMAN,4,4
1@gl_k,240,43,7	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp9	4_M_DIEMAN,4,4
1@gl_k,271,19,1	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp10	4_M_DIEMAN,4,4
1@gl_k,246,62,7	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp11	4_M_DIEMAN,4,4
1@gl_k,282,48,5	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp12	4_M_DIEMAN,4,4
1@gl_k,285,81,7	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp13	4_M_DIEMAN,4,4
1@gl_k,241,86,5	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp14	4_M_DIEMAN,4,4
1@gl_k,249,101,3	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp15	4_M_DIEMAN,4,4
1@gl_k,276,106,7	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp16	4_M_DIEMAN,4,4
1@gl_k,252,120,7	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp17	4_M_DIEMAN,4,4
1@gl_k,258,150,1	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp18	4_M_DIEMAN,4,4
1@gl_k,255,157,6	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp19	4_M_DIEMAN,4,4
1@gl_k,261,164,7	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp20	4_M_DIEMAN,4,4
1@gl_k,269,173,7	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp21	4_M_DIEMAN,4,4
1@gl_k,280,167,3	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp22	4_M_DIEMAN,4,4
1@gl_k,293,161,3	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp23	4_M_DIEMAN,4,4
1@gl_k,226,96,3	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp24	4_M_DIEMAN,4,4
1@gl_k,222,119,5	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp25	4_M_DIEMAN,4,4
1@gl_k,233,123,3	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp26	4_M_DIEMAN,4,4
2@gl_k,147,203,5	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp27	4_M_DIEMAN,4,4
2@gl_k,141,222,3	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp28	4_M_DIEMAN,4,4
2@gl_k,167,225,5	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp29	4_M_DIEMAN,4,4
2@gl_k,145,236,5	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp30	4_M_DIEMAN,4,4
2@gl_k,143,260,3	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp31	4_M_DIEMAN,4,4
2@gl_k,170,259,3	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp32	4_M_DIEMAN,4,4
2@gl_k,143,197,5	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp33	4_M_DIEMAN,4,4
2@gl_k,155,195,5	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp34	4_M_DIEMAN,4,4
2@gl_k,154,188,3	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp35	4_M_DIEMAN,4,4
2@gl_k,153,214,3	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp36	4_M_DIEMAN,4,4
2@gl_k,172,233,3	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp37	4_M_DIEMAN,4,4
2@gl_k,176,245,3	duplicate(Breathless Man#GHtramp1)	Breathless Man#GHtramp38	4_M_DIEMAN,4,4

//== Sector 3 Mobs =========================================
1@gl_k,0,0,0	script	#ghmemorialmob03	FAKE_NPC,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#ghmemorialmob03");
	end;
OnEnable:
	enablenpc instance_npcname("#ghmemorialmob03");
	donpcevent instance_npcname("#ghinstancewarp5")+"::OnEnable";
	donpcevent instance_npcname("#ghinstancewarp6")+"::OnEnable";
	.@map$ = instance_mapname("1@gl_k");
	.@label$ = instance_npcname("#ghmemorialmob03")+"::OnMyMobDead";
	mapannounce .@map$,"A portal has appeared to the northwest.",bc_map,"0xFFFF00";
	areamonster .@map$,17,259,53,180,"Decayed Guard",MG_RAYDRIC,11,.@label$;
	areamonster .@map$,17,259,53,180,"Sharpshooter Ranger",MG_RAYDRIC_ARCHER,14,.@label$;
	areamonster .@map$,62,281,73,186,"Decayed Guard",MG_RAYDRIC,11,.@label$;
	areamonster .@map$,62,281,73,186,"Sharpshooter Ranger",MG_RAYDRIC_ARCHER,14,.@label$;
	areamonster .@map$,74,251,109,224,"Decayed Guard",MG_RAYDRIC,11,.@label$;
	areamonster .@map$,74,251,109,224,"Sharpshooter Ranger",MG_RAYDRIC_ARCHER,14,.@label$;
	areamonster .@map$,108,281,231,234,"Decayed Guard",MG_RAYDRIC,11,.@label$;
	areamonster .@map$,108,281,231,234,"Sharpshooter Ranger",MG_RAYDRIC_ARCHER,14,.@label$;
	end;
OnMyMobDead:
	.@map$ = instance_mapname("1@gl_k");
	.@label$ = instance_npcname("#ghmemorialmob03")+"::OnMyMobDead";
	.@mob_dead_num = 100 - mobcount(.@map$,.@label$);
	if (.@mob_dead_num > 85) {
		mapannounce .@map$,"Himelmez: Not bad. Thought you would be dead by now by my creatures~",bc_map,"0xFFFFFF";
		killmonster .@map$,.@label$;
		donpcevent instance_npcname("#GHMclear3")+"::OnEnable";
		donpcevent instance_npcname("Heinrich#ghinstance4")+"::OnEnable";
		donpcevent instance_npcname("Himelmez#ghinstance2")+"::OnEnable";
		donpcevent instance_npcname("Varmundt#ghinstance3")+"::OnEnable";
		donpcevent instance_npcname("#ghmemorialmob03")+"::OnDisable";
	}
	end;
}

//== 1st MVP ===============================================
1@gl_k,0,0,0	script	#ghmemorialmob04	FAKE_NPC,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#ghmemorialmob04");
	end;
OnEnable:
	enablenpc instance_npcname("#ghmemorialmob04");
	.@mob_id = monster(instance_mapname("1@gl_k"),150,258,"Root of Corruption",2475,1,instance_npcname("#ghmemorialmob04")+"::OnMyMobDead");
	unittalk .@mob_id, "Grrrrrrhh~~~";
	end;
OnMyMobDead:
	.@map$ = instance_mapname("1@gl_k");
	if (mobcount(.@map$,instance_npcname("#ghmemorialmob04")+"::OnMyMobDead") < 1) {
		mapannounce .@map$,"Opening 2nd floor entrance towards 12 O'clock direction.",bc_map,"0xFFFF00";
		donpcevent instance_npcname("#ghinstancewarp7")+"::OnEnable";
		donpcevent instance_npcname("#ghinstancewarp14")+"::OnEnable";
		donpcevent instance_npcname("Varmundt#ghinstance3")+"::OnTalk2";
		donpcevent instance_npcname("#effectGH01")+"::OnEnable";
		donpcevent instance_npcname("Heinrich#ghinstance5")+"::OnEnable";
		donpcevent instance_npcname("Varmundt#ghinstance4")+"::OnEnable";
		donpcevent instance_npcname("#ghmemorialmob04")+"::OnDisable";
	}
	end;
}

1@gl_k,150,257,3	script	#GHMclear3	HIDDEN_NPC,9,9,{
	end;
OnTouch:
	donpcevent instance_npcname("#controlGH3")+"::OnEnable";
	specialeffect EF_BASH;
	donpcevent instance_npcname("#GHMclear3")+"::OnDisable";
	end;
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#GHMclear3");
	end;
OnEnable:
	enablenpc instance_npcname("#GHMclear3");
	initnpctimer;
	end;
OnTimer2000:
	mapannounce instance_mapname("1@gl_k"),"Himelmez: I'll wait for you at the north of the castle. I want to see you how lucky you are.",bc_map,"0xFFFFFF";
	stopnpctimer;
	end;
}

1@gl_k,0,0,0	script	#controlGH3	FAKE_NPC,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#controlGH3");
	end;
OnEnable:
	enablenpc instance_npcname("#controlGH3");
	initnpctimer;
	end;
OnTimer3000:
	donpcevent instance_npcname("Himelmez#ghinstance2")+"::OnTalk1";
	end;
OnTimer6000:
	donpcevent instance_npcname("Himelmez#ghinstance2")+"::OnTalk2";
	end;
OnTimer9000:
	donpcevent instance_npcname("Himelmez#ghinstance2")+"::OnTalk3";
	end;
OnTimer12000:
	donpcevent instance_npcname("Heinrich#ghinstance4")+"::OnTalk1";
	end;
OnTimer15000:
	donpcevent instance_npcname("Himelmez#ghinstance2")+"::OnTalk4";
	end;
OnTimer18000:
	donpcevent instance_npcname("Varmundt#ghinstance3")+"::OnTalk1";
	end;
OnTimer21000:
	donpcevent instance_npcname("Heinrich#ghinstance4")+"::OnTalk2";
	end;
OnTimer24000:
	donpcevent instance_npcname("Himelmez#ghinstance2")+"::OnTalk5";
	end;
OnTimer27000:
	donpcevent instance_npcname("Himelmez#ghinstance2")+"::OnTalk6";
	end;
OnTimer28000:
	donpcevent instance_npcname("Himelmez#ghinstance2")+"::OnDisable";
	end;
OnTimer31000:
	donpcevent instance_npcname("Heinrich#ghinstance4")+"::OnTalk3";
	end;
OnTimer32000:
	donpcevent instance_npcname("Heinrich#ghinstance4")+"::OnDisable";
	end;
OnTimer35000:
	mapannounce instance_mapname("1@gl_k"),"An echoing comes from deep inside of the knight's shrine.",bc_map,"0xFFFFFF";
	end;
OnTimer38000:
	donpcevent instance_npcname("#ghmemorialmob04")+"::OnEnable";
	stopnpctimer;
	donpcevent instance_npcname("#controlGH3")+"::OnDisable";
	end;
}

1@gl_k,144,258,6	script	Heinrich#ghinstance4	4_M_HEINRICH,{
	cutin "gl_heinrich1",2;
	mes "[Heinrich]";
	mes "Himelmez... I will never forget what you've done to my men.";
	close2;
	cutin "gl_heinrich1",255;
	end;
OnInstanceInit:
OnDisable:
	hideonnpc instance_npcname("Heinrich#ghinstance4");
	end;
OnEnable:
	hideoffnpc instance_npcname("Heinrich#ghinstance4");
	end;
OnTalk1:
	npctalk "Himelmez! We will not let you get away from us!";
	end;
OnTalk2:
	npctalk "What... These are!";
	end;
OnTalk3:
	npctalk "Sir. Varmundt! Help those adventurers. I will go after Himelmez!";
	end;
}

1@gl_k,150,257,3	script	Himelmez#ghinstance2	4_F_HIMEL,{
	cutin "gl_himel2",2;
	mes "[Himelmez]";
	mes "Don't look at me nervous like that.";
	mes "It will be over soon...";
	close2;
	cutin "gl_himel2",255;
	end;
OnInstanceInit:
OnDisable:
	hideonnpc instance_npcname("Himelmez#ghinstance2");
	end;
OnEnable:
	hideoffnpc instance_npcname("Himelmez#ghinstance2");
	end;
OnTalk1:
	npctalk "Guess you are either lucky or powerful, to make it this far.";
	end;
OnTalk2:
	npctalk "But, it doesn't matter.";
	end;
OnTalk3:
	npctalk "You will all die here.";
	end;
OnTalk4:
	npctalk "Ha ha ha, worry about yourself. You think I am alone here?";
	end;
OnTalk5:
	npctalk "I will go on my way while my new toy entertains you.";
	end;
OnTalk6:
	npctalk "Fare well~, hope to see you again sometime, Heinrich.";
	end;
}

1@gl_k,156,259,3	script	Varmundt#ghinstance3	4_M_BARMUND,{
	if (questprogress(12318,HUNTING) == 2) {
		cutin "gl_barmund1",2;
		mes "[Varmundt]";
		mes "This item has extraordinary aura with it. Can be very useful for someone special.";
		erasequest 12318;
		if (questprogress(12319,HUNTING) == 0)
			setquest 12319;
		getitem Temporal_Crystal, 1;
		getitem Coagulated_Spell, 1;
		getexp 250000, 250000;
		close2;
		cutin "gl_barmund1",255;
		end;
	}
	mes "[Varmundt]";
	mes "Even after all this time, it is impossible to prevent from the will of the Transcendent.";
	cutin "gl_barmund1",2;
	close2;
	cutin "gl_barmund2",255;
	end;
OnInstanceInit:
OnDisable:
	hideonnpc instance_npcname("Varmundt#ghinstance3");
	end;
OnEnable:
	hideoffnpc instance_npcname("Varmundt#ghinstance3");
	end;
OnTalk1:
	npctalk "Heinrich Sir! Something unknown holds an inexplicably powerful force!";
	end;
OnTalk2:
	npctalk "From the monster I picked up some great stuff. People who are interested, talk to me.";
	end;
}

//== Floor 2 ===============================================
2@gl_k,148,67,1	script	Heinrich#ghinstance5	4_M_HEINRICH,{
	mes "[Heinrich]";
	mes "Himelmez's blocking spells";
	mes "are everywhere around here...";
	cutin "gl_heinrich1",2;
	close2;
	cutin "gl_heinrich1",255;
	end;
OnInstanceInit:
OnDisable:
	hideonnpc instance_npcname("Heinrich#ghinstance5");
	end;
OnEnable:
	hideoffnpc instance_npcname("Heinrich#ghinstance5");
	end;
OnTalk1:
	npctalk "These things have never been in the castle!";
	end;
OnTalk2:
	npctalk "Trying to break this power with common weapons doesn't work. I tried it.";
	end;
OnTalk3:
	npctalk "It is really unforgivable.";
	end;
OnTalk4:
	npctalk "I, who already was a subordinate of genocide. How much more in the future...";
	end;
OnTalk5:
	npctalk "...";
	end;
}

2@gl_k,151,71,7	script	Varmundt#ghinstance4	4_M_BARMUND,{
	mes "[Varmundt]";
	mes "Why am I doing this again? Doesn't seem like the first time. I've seen this place repeatedly dozens of times in my dreams. This feeling...";
	cutin "gl_barmund1",2;
	close2;
	cutin "gl_barmund1",255;
	end;
OnInstanceInit:
OnDisable:
	hideonnpc instance_npcname("Varmundt#ghinstance4");
	end;
OnEnable:
	hideoffnpc instance_npcname("Varmundt#ghinstance4");
	end;
OnTalk1:
	npctalk "Varmundt : This is Himelmez's closed space. We blocked each area and aisle.";
	end;
OnTalk2:
	npctalk "Varmundt : Step back. Let me try to dispel the magic.";
	end;
OnTalk3:
	npctalk "Varmundt : I think that part of closed space is broke down.";
	end;
OnTalk4:
	npctalk "Varmundt : I have never seen this closed space.";
	end;
OnTalk5:
	npctalk "Varmundt : Himelmez hides stone chains in undead people's body.";
	end;
OnTalk6:
	npctalk "Varmundt : Maybe this closed space is maintained with these stone chains.";
	end;
OnTalk7:
	npctalk "Varmundt : If you want to break it, you have to kill one of those who have a stone chain.";
	end;
OnTalk8:
	npctalk "Varmundt : But, we can't recognize who has a stone chain so, we have to purify everything.";
	end;
OnTalk9:
	npctalk "Varmundt : Commander...";
	end;
OnTalk10:
	npctalk "Varmundt : Commander! You need to be strong.";
	end;
OnTalk11:
	npctalk "Varmundt : Our enemy is not human.";
	end;
OnTalk12:
	npctalk "Varmundt : Even though you deny it, they'll never turn back to human.";
	end;
OnTalk13:
	npctalk "Varmundt : Well, let's go then.";
	end;
OnTalk14:
	npctalk "Varmundt : And you guys, just take a break and follow us.";
	end;
OnTalk15:
	npctalk "Varmundt : It could be hard fighting so, stay strong.";
	end;
OnTalk16:
	npctalk "Varmundt : Ok, it's time to begin Heinrich.";
	end;
}

2@gl_k,150,67,0	script	#effectGH01	HIDDEN_NPC,10,10,{
	end;
OnTouch:
	if (.touch) {
		.touch = 0;
		specialeffect EF_BASH;
		donpcevent instance_npcname("#controlGH4")+"::OnEnable";
	}
	end;
OnEffect:
	specialeffect EF_LORD;
	end;
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#effectGH01");
	end;
OnEnable:
	enablenpc instance_npcname("#effectGH01");
	set .touch,1;	//Activates OnTouch Only Once.
	end;
}

2@gl_k,0,0,0	script	#controlGH4	FAKE_NPC,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#controlGH4");
	end;
OnEnable:
	enablenpc instance_npcname("#controlGH4");
	initnpctimer;
	end;
OnTimer3000:
	donpcevent instance_npcname("Heinrich#ghinstance5")+"::OnTalk1";
	donpcevent instance_npcname("#ghmemorialmob05")+"::OnEnable";
	end;
OnTimer6000:
	donpcevent instance_npcname("Varmundt#ghinstance4")+"::OnTalk1";
	end;
OnTimer9000:
	donpcevent instance_npcname("Heinrich#ghinstance5")+"::OnTalk2";
	end;
OnTimer12000:
	donpcevent instance_npcname("Varmundt#ghinstance4")+"::OnTalk2";
	end;
OnTimer15000:
	donpcevent instance_npcname("#effectGH01")+"::OnEffect";
	end;
OnTimer18000:
	donpcevent instance_npcname("#effectGH01")+"::OnDisable";
	donpcevent instance_npcname("Varmundt#ghinstance4")+"::OnTalk3";
	end;
OnTimer21000:
	donpcevent instance_npcname("Varmundt#ghinstance4")+"::OnTalk4";
	end;
OnTimer24000:
	donpcevent instance_npcname("Varmundt#ghinstance4")+"::OnTalk5";
	end;
OnTimer27000:
	donpcevent instance_npcname("Varmundt#ghinstance4")+"::OnTalk6";
	end;
OnTimer30000:
	donpcevent instance_npcname("Varmundt#ghinstance4")+"::OnTalk7";
	end;
OnTimer33000:
	donpcevent instance_npcname("Varmundt#ghinstance4")+"::OnTalk8";
	end;
OnTimer36000:
	donpcevent instance_npcname("Heinrich#ghinstance5")+"::OnTalk3";
	end;
OnTimer39000:
	donpcevent instance_npcname("Heinrich#ghinstance5")+"::OnTalk4";
	end;
OnTimer42000:
	donpcevent instance_npcname("Varmundt#ghinstance4")+"::OnTalk9";
	end;
OnTimer45000:
	donpcevent instance_npcname("Varmundt#ghinstance4")+"::OnTalk10";
	end;
OnTimer48000:
	donpcevent instance_npcname("Varmundt#ghinstance4")+"::OnTalk11";
	end;
OnTimer51000:
	donpcevent instance_npcname("Varmundt#ghinstance4")+"::OnTalk12";
	end;
OnTimer54000:
	donpcevent instance_npcname("Heinrich#ghinstance5")+"::OnTalk5";
	end;
OnTimer57000:
	donpcevent instance_npcname("Varmundt#ghinstance4")+"::OnTalk13";
	end;
OnTimer60000:
	donpcevent instance_npcname("Varmundt#ghinstance4")+"::OnTalk14";
	end;
OnTimer63000:
	donpcevent instance_npcname("Varmundt#ghinstance4")+"::OnTalk15";
	end;
OnTimer66000:
	donpcevent instance_npcname("Varmundt#ghinstance4")+"::OnTalk16";
	end;
OnTimer69000:
	mapannounce instance_mapname("2@gl_k"),"A portal to the west has opened in the central hallway.",bc_map,"0xFFFF00";
	donpcevent instance_npcname("#ghinstancewarp8")+"::OnEnable";
	donpcevent instance_npcname("#ghinstancewarp9")+"::OnEnable";
	donpcevent instance_npcname("#ghmemorialmob06")+"::OnEnable";
	donpcevent instance_npcname("Varmundt#ghinstance4")+"::OnDisable";
	donpcevent instance_npcname("Heinrich#ghinstance5")+"::OnDisable";
	end;
OnTimer70000:
	stopnpctimer;
	donpcevent instance_npcname("#controlGH4")+"::OnDisable";
	end;
}

2@gl_k,0,0,0	script	#ghmemorialmob05	FAKE_NPC,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#ghmemorialmob05");
	end;
OnEnable:
	enablenpc instance_npcname("#ghmemorialmob05");
	.@map$ = instance_mapname("2@gl_k");
	.@label$ = instance_npcname("#ghmemorialmob05")+"::OnMyMobDead";
	areamonster .@map$,124,20,31,162,"Decayed Guard",MG_RAYDRIC,8,.@label$;
	areamonster .@map$,124,20,31,162,"Sharpshooter Ranger",MG_RAYDRIC_ARCHER,8,.@label$;
	areamonster .@map$,124,20,31,162,"Fallen Abyss Knight",MG_KNIGHT_OF_ABYSS,8,.@label$;
	areamonster .@map$,124,20,31,162,"Suffering Khalitzburg",MG_KHALITZBURG,8,.@label$;
	areamonster .@map$,124,20,31,162,"Swollen Knight",MG_BLOODY_KNIGHT,8,.@label$;
	.MyMobs = 80;
	end;
OnMyMobDead:
	.@map$ = instance_mapname("2@gl_k");
	--.MyMobs;
	if (.MyMobs == 0) {
		mapannounce .@map$,"Evil Forces are appearing in this area.",bc_map,"0xFFFF00",FW_NORMAL,18;
		donpcevent instance_npcname("#ghmemorialmob06")+"::OnDisable2";
		donpcevent instance_npcname("#ghmemorialmob07")+"::OnEnable";
		donpcevent instance_npcname("#ghmemorialmob05")+"::OnDisable";
	} else {
		.@label$ = instance_npcname("#ghmemorialmob05")+"::OnMyMobDead";
		switch(rand(5)) {
		case 0:
			areamonster .@map$,124,20,31,162,"Decayed Guard",MG_RAYDRIC,1,.@label$;
			break;
		case 1:
			areamonster .@map$,124,20,31,162,"Sharpshooter Ranger",MG_RAYDRIC_ARCHER,1,.@label$;
			break;
		case 2:
			areamonster .@map$,124,20,31,162,"Fallen Abyss Knight",MG_KNIGHT_OF_ABYSS,1,.@label$;
			break;
		case 3:
			areamonster .@map$,124,20,31,162,"Suffering Khalitzburg",MG_KHALITZBURG,1,.@label$;
			break;
		case 4:
			areamonster .@map$,124,20,31,162,"Swollen Knight",MG_BLOODY_KNIGHT,1,.@label$;
			break;
		}
	}
	end;
}

2@gl_k,0,0,0	script	#ghmemorialmob06	FAKE_NPC,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#ghmemorialmob06");
	end;
OnDisable2:
	stopnpctimer;
	disablenpc instance_npcname("#ghmemorialmob06");
	end;
OnEnable:
	enablenpc instance_npcname("#ghmemorialmob06");
	donpcevent instance_npcname("#ghmemorialmob06")+"::OnSpawn";
	end;
OnSpawn:
	initnpctimer;
	end;
OnTimer90000:
	.@map$ = instance_mapname("2@gl_k");
	.@label$ = instance_npcname("#ghmemorialmob05")+"::OnMyMobDead";
	setarray .@c[0],
		114,141,118,139,
		128,83,131,78,
		88,53,93,48,
		54,47,61,40,
		58,83,63,78,
		69,138,80,127,
		34,143,39,138;
	for(.@i = 0; .@i<getarraysize(.@c); .@i += 4) {
		areamonster .@map$,.@c[.@i],.@c[.@i+1],.@c[.@i+2],.@c[.@i+3],"Decayed Guard",MG_RAYDRIC,1,.@label$;
		areamonster .@map$,.@c[.@i],.@c[.@i+1],.@c[.@i+2],.@c[.@i+3],"Sharpshooter Ranger",MG_RAYDRIC_ARCHER,1,.@label$;
		areamonster .@map$,.@c[.@i],.@c[.@i+1],.@c[.@i+2],.@c[.@i+3],"Fallen Abyss Knight",MG_KNIGHT_OF_ABYSS,1,.@label$;
		areamonster .@map$,.@c[.@i],.@c[.@i+1],.@c[.@i+2],.@c[.@i+3],"Suffering Khalitzburg",MG_KHALITZBURG,1,.@label$;
		areamonster .@map$,.@c[.@i],.@c[.@i+1],.@c[.@i+2],.@c[.@i+3],"Swollen Knight",MG_BLOODY_KNIGHT,1,.@label$;
	}
	end;
OnTimer92000:
	stopnpctimer;
	donpcevent instance_npcname("#ghmemorialmob06")+"::OnSpawn";
	end;
}

2@gl_k,0,0,0	script	#ghmemorialmob07	FAKE_NPC,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#ghmemorialmob07");
	end;
OnEnable:
	enablenpc instance_npcname("#ghmemorialmob07");
	areamonster instance_mapname("2@gl_k"),124,20,31,162,"The Destruction of the First Captain",2473,1,instance_npcname("#ghmemorialmob07")+"::OnMyMobDead";
	end;
OnMyMobDead:
	.@map$ = instance_mapname("2@gl_k");
	if (mobcount(.@map$,instance_npcname("#ghmemorialmob07")+"::OnMyMobDead") < 1) {
		mapannounce .@map$,"A portal has opened to the east in the central hallway.",bc_map,"0xFFFF00";
		donpcevent instance_npcname("#ghinstancewarp10")+"::OnEnable";
		donpcevent instance_npcname("#ghinstancewarp11")+"::OnEnable";
		donpcevent instance_npcname("#ghmemorialmob08")+"::OnEnable";
		donpcevent instance_npcname("#ghmemorialmob07")+"::OnDisable";
	}
	end;
}

2@gl_k,0,0,0	script	#ghmemorialmob08	FAKE_NPC,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#ghmemorialmob08");
	end;
OnEnable:
	enablenpc instance_npcname("#ghmemorialmob08");
	.@map$ = instance_mapname("2@gl_k");
	.@label$ = instance_npcname("#ghmemorialmob08")+"::OnMyMobDead";
	areamonster .@map$,175,163,265,18,"Decayed Guard",MG_RAYDRIC,7,.@label$;
	areamonster .@map$,175,163,265,18,"Sharpshooter Ranger",MG_RAYDRIC_ARCHER,7,.@label$;
	areamonster .@map$,175,163,265,18,"Fallen Abyss Knight",MG_KNIGHT_OF_ABYSS,7,.@label$;
	areamonster .@map$,175,163,265,18,"Suffering Khalitzburg",MG_KHALITZBURG,7,.@label$;
	areamonster .@map$,175,163,265,18,"Swollen Knight",MG_BLOODY_KNIGHT,7,.@label$;
	.MyMobs = 35;
	end;
OnMyMobDead:
	.@map$ = instance_mapname("2@gl_k");
	--.MyMobs;
	if (.MyMobs == 0) {
		mapannounce .@map$,"An evil presence has teleported into this area.",bc_map,"0xFFFF00",FW_NORMAL,18;
		donpcevent instance_npcname("#ghmemorialmob09")+"::OnEnable";
		donpcevent instance_npcname("#ghmemorialmob08")+"::OnDisable";
	} else {
		.@label$ = instance_npcname("#ghmemorialmob08")+"::OnMyMobDead";
		switch(rand(5)) {
		case 0:
			areamonster .@map$,175,163,265,18,"Decayed Guard",MG_RAYDRIC,1,.@label$;
			break;
		case 1:
			areamonster .@map$,175,163,265,18,"Sharpshooter Ranger",MG_RAYDRIC_ARCHER,1,.@label$;
			break;
		case 2:
			areamonster .@map$,175,163,265,18,"Fallen Abyss Knight",MG_KNIGHT_OF_ABYSS,1,.@label$;
			break;
		case 3:
			areamonster .@map$,175,163,265,18,"Suffering Khalitzburg",MG_KHALITZBURG,1,.@label$;
			break;
		case 4:
			areamonster .@map$,175,163,265,18,"Swollen Knight",MG_BLOODY_KNIGHT,1,.@label$;
			break;
		}
	}
	end;
}

2@gl_k,0,0,0	script	#ghmemorialmob09	FAKE_NPC,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#ghmemorialmob09");
	end;
OnEnable:
	enablenpc instance_npcname("#ghmemorialmob09");
	areamonster instance_mapname("2@gl_k"),175,163,265,18,"The Destruction of the Second Captain",2474,1,instance_npcname("#ghmemorialmob09")+"::OnMyMobDead";
	end;
OnMyMobDead:
	.@map$ = instance_mapname("2@gl_k");
	if (mobcount(.@map$,instance_npcname("#ghmemorialmob09")+"::OnMyMobDead") < 1) {
		mapannounce .@map$, "12 o'clock positioned warp leading to the 3rd zone has been opened. Use the central hallway to get there.",bc_map,"0xFFFF00";
		donpcevent instance_npcname("#ghinstancewarp12")+"::OnEnable";
		donpcevent instance_npcname("#ghinstancewarp13")+"::OnEnable";
		donpcevent instance_npcname("#controlGH6")+"::OnEnable";
		donpcevent instance_npcname("#ghmemorialmob08")+"::OnDisable";
		donpcevent instance_npcname("#ghmemorialmob09")+"::OnDisable";
	}
	end;
}

2@gl_k,0,0,0	script	#ghmemorialmob10	FAKE_NPC,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#ghmemorialmob10");
	end;
OnEnable:
	enablenpc instance_npcname("#ghmemorialmob10");
	donpcevent instance_npcname("Gerhalt#ghinstance1")+"::OnDisable";
	.mob_id = monster(instance_mapname("2@gl_k"),158,255,"Amdarias",2476,1,instance_npcname("#ghmemorialmob10")+"::OnMyMobDead");
	unittalk .mob_id, "Run away... run away from me...";
	initnpctimer;
	end;
OnTimer6000:
	unittalk .mob_id, "I don't want... I don't want to kill anyone. Uhuuuuuh";
	end;
OnTimer12000:
	unittalk .mob_id, "Please kill me! Please!";
	end;
OnTimer18000:
	unittalk .mob_id, "Eeeeee...eee...die... die...";
	end;
OnTimer24000:
	unittalk .mob_id, "Demolition... Death!...";
	stopnpctimer;
	end;
OnMyMobDead:
	if (mobcount(instance_mapname("2@gl_k"),instance_npcname("#ghmemorialmob10")+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("Hugin#ghinstance1")+"::OnEnable";
		donpcevent instance_npcname("#ghinstancewarp15")+"::OnEnable";
		
		for (.@i = 1; .@i <= 8; ++.@i)
			enablenpc instance_npcname("Strange crack#"+.@i);

		stopnpctimer;
		donpcevent instance_npcname("#ghmemorialmob10")+"::OnDisable";
	}
	end;
}

2@gl_k,155,250,7	script	Heinrich#ghinstance6	4_M_HEINRICH,{
	cutin "gl_heinrich1",2;
	mes "[Heinrich]";
	mes "What are you decorating Himelmez!";
	close2;
	cutin "gl_heinrich1",255;
	end;
OnInstanceInit:
OnDisable:
	hideonnpc instance_npcname("Heinrich#ghinstance6");
	end;
OnEnable:
	hideoffnpc instance_npcname("Heinrich#ghinstance6");
	end;
OnTalk1:
	npctalk "Gerhalt!";
	end;
OnTalk2:
	npctalk "What are you doing to my men, Himelmez?!";
	end;
OnTalk3:
	npctalk "Himelmez! You don't need to make any more sacrifices!";
	end;
OnTalk4:
	npctalk "Let him go! I don't want one more sacrifice!";
	end;
OnTalk5:
	npctalk "I will not forgive you.";
	end;
OnTalk6:
	npctalk "Just leave us alone, Himelmez!!";
	end;
}

2@gl_k,162,250,1	script	Varmundt#ghinstance5	4_M_BARMUND,{
	mes "[Varmundt]";
	mes "We can't escape this";
	mes "eternal confinement...";
	cutin "gl_barmund2",2;
	close2;
	cutin "gl_barmund2",255;
	end;
OnInstanceInit:
OnDisable:
	hideonnpc instance_npcname("Varmundt#ghinstance5");
	end;
OnEnable:
	hideoffnpc instance_npcname("Varmundt#ghinstance5");
	end;
OnTalk1:
	npctalk "What? This cannot be. We must prevent Amdarias's attacks!";
	end;
}

2@gl_k,158,252,3	script	Himelmez#ghinstance4	4_F_HIMEL,4,4,{
	end;
OnInstanceInit:
OnDisable:
	hideonnpc instance_npcname("Himelmez#ghinstance4");
	end;
OnEnable:
	hideoffnpc instance_npcname("Himelmez#ghinstance4");
	end;
OnTalk1:
	npctalk "Great~ I thought you wouldn't come near the end...";
	end;
OnTalk2:
	npctalk "Huhu, I have already found a piece of Ymir's Heart, Heinrich.";
	end;
OnTalk3:
	npctalk "It would've been faster if there were no distractions.";
	end;
OnTalk4:
	npctalk "What do you think? Making it look like an accidental disease infected the king and the people around...";
	end;
OnTalk5:
	npctalk "You want that?";
	end;
OnTalk6:
	npctalk "This one is your final blow, Heinrich.";
	end;
OnTalk7:
	npctalk "It is said that stopping me requires a skillfull person.";
	end;
OnTalk8:
	npctalk "Seems like a perfect scenario to make my new monster, Amdarias.";
	end;
OnTalk9:
	npctalk "Booh~ I'm scared.";
	end;
OnTalk10:
	npctalk "Anyway, it was nice talking to you. Maybe we'll have the chance to meet again in the next story.";
	end;
OnTalk11:
	npctalk "Well, make sure you will come back again.";
	end;
}

2@gl_k,150,179,0	script	#controlGH6	HIDDEN_NPC,2,2,{
end;
OnTouch:
	mapannounce instance_mapname("2@gl_k"),"???: Do not come here! It's a trap... Kkkkah!!!",bc_map,"0xFF0000",FW_NORMAL,18;
	specialeffect EF_BASH;
	donpcevent instance_npcname("Himelmez#ghinstance3")+"::OnEnable";
	donpcevent instance_npcname("Heinrich#ghinstance6")+"::OnEnable";
	donpcevent instance_npcname("Varmundt#ghinstance5")+"::OnEnable";
	donpcevent instance_npcname("Gerhalt#ghinstance1")+"::OnEnable";
	donpcevent instance_npcname("#controlGH6")+"::OnDisable";
	end;
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#controlGH6");
	end;
OnEnable:
	enablenpc instance_npcname("#controlGH6");
	end;
}

2@gl_k,158,252,1	script	Himelmez#ghinstance3	4_F_HIMEL,7,7,{
	end;
OnTouch:
	specialeffect EF_BASH;
	donpcevent instance_npcname("#controlGH5")+"::OnEnable";
	donpcevent instance_npcname("Himelmez#ghinstance4")+"::OnEnable";
	donpcevent instance_npcname("Himelmez#ghinstance3")+"::OnDisable";
	end;
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("Himelmez#ghinstance3");
	end;
OnEnable:
	enablenpc instance_npcname("Himelmez#ghinstance3");
	end;
}

2@gl_k,158,255,3	script	Gerhalt#ghinstance1	4_LEVITATEMAN,{
	mes "[Gerhalt]";
	mes "Uuuuu... Khhhah! Just run away with the commander.";
	close;
OnInstanceInit:
OnDisable:
	hideonnpc instance_npcname("Gerhalt#ghinstance1");
	end;
OnEnable:
	hideoffnpc instance_npcname("Gerhalt#ghinstance1");
	end;
OnTalk1:
	npctalk "Kkkkah! Run away! I can't endure anymore!";
	end;
OnTalk2:
	npctalk "Commandant... Come on, you need to run away from here... Ugh.";
	end;
OnTalk3:
	npctalk "You can make my body but you can't take my soul Himelmez!";
	end;
OnEffect1:
	specialeffect EF_BARRIER;
	end;
OnEffect2:
	specialeffect EF_CHAINCOMBO;
	end;
OnEffect3:
	specialeffect EF_MAPPILLAR2;
	end;
OnEffect4:
	specialeffect EF_MAPPILLAR;
	end;
OnEffect5:
	specialeffect EF_LORD;
	end;
}

2@gl_k,0,0,0	script	#controlGH5	FAKE_NPC,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#controlGH5");
	end;
OnEnable:
	enablenpc instance_npcname("#controlGH5");
	initnpctimer;
	end;
OnTimer3000:
	donpcevent instance_npcname("Himelmez#ghinstance4")+"::OnTalk1";
	end;
OnTimer6000:
	donpcevent instance_npcname("Gerhalt#ghinstance1")+"::OnTalk1";
	end;
OnTimer9000:
	donpcevent instance_npcname("Heinrich#ghinstance6")+"::OnTalk1";
	end;
OnTimer15000:
	donpcevent instance_npcname("Heinrich#ghinstance6")+"::OnTalk2";
	end;
OnTimer18000:
	donpcevent instance_npcname("Himelmez#ghinstance4")+"::OnTalk2";
	end;
OnTimer21000:
	donpcevent instance_npcname("Himelmez#ghinstance4")+"::OnTalk3";
	end;
OnTimer24000:
	donpcevent instance_npcname("Gerhalt#ghinstance1")+"::OnTalk2";
	end;
OnTimer27000:
	donpcevent instance_npcname("Himelmez#ghinstance4")+"::OnTalk4";
	end;
OnTimer30000:
	donpcevent instance_npcname("Heinrich#ghinstance6")+"::OnTalk3";
	end;
OnTimer33000:
	donpcevent instance_npcname("Heinrich#ghinstance6")+"::OnTalk4";
	end;
OnTimer36000:
	donpcevent instance_npcname("Himelmez#ghinstance4")+"::OnTalk5";
	end;
OnTimer39000:
	donpcevent instance_npcname("Himelmez#ghinstance4")+"::OnTalk6";
	end;
OnTimer42000:
	donpcevent instance_npcname("Himelmez#ghinstance4")+"::OnTalk7";
	end;
OnTimer45000:
	donpcevent instance_npcname("Himelmez#ghinstance4")+"::OnTalk8";
	end;
OnTimer48000:
	donpcevent instance_npcname("Heinrich#ghinstance6")+"::OnTalk5";
	end;
OnTimer51000:
	donpcevent instance_npcname("Himelmez#ghinstance4")+"::OnTalk9";
	end;
OnTimer54000:
	donpcevent instance_npcname("Himelmez#ghinstance4")+"::OnTalk10";
	end;
OnTimer57000:
	donpcevent instance_npcname("Himelmez#ghinstance4")+"::OnTalk11";
	end;
OnTimer60000:
	donpcevent instance_npcname("Gerhalt#ghinstance1")+"::OnEffect1";
	end;
OnTimer63000:
	donpcevent instance_npcname("Himelmez#ghinstance4")+"::OnDisable";
	end;
OnTimer65000:
	donpcevent instance_npcname("Gerhalt#ghinstance1")+"::OnEffect3";
	end;
OnTimer66000:
	donpcevent instance_npcname("Gerhalt#ghinstance1")+"::OnEffect2";
	end;
OnTimer67000:
	donpcevent instance_npcname("Heinrich#ghinstance6")+"::OnTalk6";
	donpcevent instance_npcname("Gerhalt#ghinstance1")+"::OnEffect4";
	end;
OnTimer70000:
	donpcevent instance_npcname("Gerhalt#ghinstance1")+"::OnEffect3";
	donpcevent instance_npcname("Gerhalt#ghinstance1")+"::OnEffect4";
	donpcevent instance_npcname("Heinrich#ghinstance6")+"::OnDisable";
	donpcevent instance_npcname("Varmundt#ghinstance5")+"::OnTalk1";
	donpcevent instance_npcname("Varmundt#ghinstance5")+"::OnDisable";
	end;
OnTimer73000:
	mapannounce instance_mapname("2@gl_k"),"Gerhalt's body changing.",bc_map,"0xFFFFFF";
	end;
OnTimer76000:
	donpcevent instance_npcname("Gerhalt#ghinstance1")+"::OnEffect5";
	end;
OnTimer80000:
	donpcevent instance_npcname("#ghmemorialmob10")+"::OnEnable";
	stopnpctimer;
	donpcevent instance_npcname("#controlGH5")+"::OnDisable";
	end;
}

2@gl_k,158,241,1	script	Hugin#ghinstance1	4_M_SAGE_C,{
	if (questprogress(12319,HUNTING) == 2) {
		mes "[Hugin]";
		mes "Hm, you are very well. First of all, let me give you some loot from Amdarais.";
		erasequest 12319;
		setquest 12322;
		if (gh_instance == 1) {
			getitem Temporal_Crystal, 1;
			getitem Coagulated_Spell, 1;
		} else {
			getitem Temporal_Crystal, 5;
			getitem Coagulated_Spell, 5;
			gh_instance = 1;
		}
		getexp 350000, 350000;
		next;
		mes "[Hugin]";
		mes "Varmundt's time is stopped by me.";
		mes "And your time will be distorted soon.";
		specialeffect2 EF_BLIND,AREA;
		soundeffect "_blind.wav",0;
		next;
		mes "[Hugin]";
		mes "Maybe this poor time traveler will try to stop Glast Heim's tragedy from happening.";
		next;
		mes "[Hugin]";
		mes "However, we will never overlook his behavior now and forever...";
		next;
		mes "[Hugin]";
		mes "Now, let me remove your memory. If you see me again, that will be brand new.";
		specialeffect2 EF_FREEZE;
		close2;
		getmapxy(.@map$, .@x, .@y, UNITTYPE_PC);
		warp .@map$,.@x,.@y;
		end;
	} else {
		mes "[Hugin]";
		mes "Oops I almost distorted the time gap. Come over here. We need to go out!";
		next;
		select("You were just with me...");
		mes "[Hugin]";
		mes "What did you say just before?";
		mes "Anyway that is not important, The gap of time will be closed so, we need to get out of here.";
		next;
		switch(select("Let me look around more", "Please let me out")) {
		case 1:
			mes "[Hugin]";
			mes "Really? This place will be broke down so, please look around quickly.";
			close;
		case 2:
			close2;
			warp "glast_01",204,270;
			end;
		}
	}
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("Hugin#ghinstance1");
	end;
OnEnable:
	enablenpc instance_npcname("Hugin#ghinstance1");
	end;
}

// Treasure Room
//============================================================
1@gl_k,269,267,0	script	Strange crack#entrace	HIDDEN_NPC,{
	if (questprogress(12322) == 0) {
		mes "The crack looks suspicious, but nothing more to check.";
		close;
	}
	.@map$ = instance_mapname("1@gl_k");
	warp .@map$,149,198;
	end;
}

1@gl_k,129,136,0	script	Strange crack#1	HIDDEN_NPC,{
	specialeffect EF_SPELLBREAKER;
	specialeffect EF_BASH;
	.@random = rand(1, 4);
	for (.@i = 1; .@i <= .@random; ++.@i)
		makeitem White_Jewel,1,"this",129,138;
	makeitem Coagulated_Spell,1,"this",129,138;
	.@random = rand(1, 4);
	if (.@random == 4)
		makeitem Heavy_Sword,1,"this",129,138;
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("Strange crack#1");
	end;
OnEnable:
	enablenpc instance_npcname("Strange crack#1");
	end;
}

1@gl_k,135,136,0	script	Strange crack#2	HIDDEN_NPC,{
	specialeffect EF_SPELLBREAKER;
	specialeffect EF_BASH;
	.@random = rand(1, 4);
	for (.@i = 1; .@i <= .@random; ++.@i)
		makeitem Blue_Jewel,1,"this",135,138;
	.@random = rand(1, 4);
	if (.@random == 4)
		makeitem Staff_Of_Geffen,1,"this",135,138;
	makeitem Coagulated_Spell,1,"this",135,138;
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("Strange crack#2");
	end;
OnEnable:
	enablenpc instance_npcname("Strange crack#2");
	end;
}

1@gl_k,141,136,0	script	Strange crack#3	HIDDEN_NPC,{
	specialeffect EF_SPELLBREAKER;
	specialeffect EF_BASH;
	.@random = rand(1, 4);
	for (.@i = 1; .@i <= .@random; ++.@i)
		makeitem Red_Jewel,1,"this",141,138;
	makeitem Coagulated_Spell,1,"this",141,138;
	makeitem Gold_Bullion,1,"this",141,138;
	.@random = rand(1, 4);
	if (.@random == 4)
		makeitem Ceremonial_Sword,1,"this",141,138;
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("Strange crack#3");
	end;
OnEnable:
	enablenpc instance_npcname("Strange crack#3");
	end;
}

1@gl_k,147,136,0	script	Strange crack#4	HIDDEN_NPC,{
	specialeffect EF_SPELLBREAKER;
	specialeffect EF_BASH;
	.@random = rand(1, 4);
	for (.@i = 1; .@i <= .@random; ++.@i)
		makeitem Scarlet_Jewel,1,"this",147,138;
	.@random = rand(1, 4);
	if (.@random == 4)
		makeitem Silversmith_Bracelet,1,"this",147,138;
	makeitem Coagulated_Spell,1,"this",147,138;
	makeitem Gold_Coin_Basket,1,"this",147,138;
	makeitem Colorful_Brooch,1,"this",147,138;
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("Strange crack#4");
	end;
OnEnable:
	enablenpc instance_npcname("Strange crack#4");
	end;
}

1@gl_k,153,136,0	script	Strange crack#5	HIDDEN_NPC,{
	specialeffect EF_SPELLBREAKER;
	specialeffect EF_BASH;
	.@random = rand(1, 4);
	for (.@i = 1; .@i <= .@random; ++.@i)
		makeitem Azure_Jewel,1,"this",153,138;
	makeitem Coagulated_Spell,1,"this",153,138;
	makeitem White_Gold_Bullion,1,"this",153,138;
	.@random = rand(1, 4);
	if (.@random == 4)
		makeitem Greater_Lease,1,"this",153,138;
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("Strange crack#5");
	end;
OnEnable:
	enablenpc instance_npcname("Strange crack#5");
	end;
}

1@gl_k,159,136,0	script	Strange crack#6	HIDDEN_NPC,{
	specialeffect EF_SPELLBREAKER;
	specialeffect EF_BASH;
	.@random = rand(1, 4);
	for (.@i = 1; .@i <= .@random; ++.@i)
		makeitem Skyblue_Jewel,1,"this",159,138;
	makeitem Coagulated_Spell,1,"this",159,138;
	makeitem Silver_Bullion,1,"this",159,138;
	.@random = rand(1, 4);
	if (.@random == 4)
		makeitem Engraved_Armor,1,"this",159,138;
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("Strange crack#6");
	end;
OnEnable:
	enablenpc instance_npcname("Strange crack#6");
	end;
}

1@gl_k,165,136,0	script	Strange crack#7	HIDDEN_NPC,{
	specialeffect EF_SPELLBREAKER;
	specialeffect EF_BASH;
	.@random = rand(1, 4);
	for (.@i = 1; .@i <= .@random; ++.@i)
		makeitem Violet_Jewel,1,"this",165,138;
	makeitem Coagulated_Spell,1,"this",165,138;
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("Strange crack#7");
	end;
OnEnable:
	enablenpc instance_npcname("Strange crack#7");
	end;
}

1@gl_k,171,136,0	script	Strange crack#8	HIDDEN_NPC,{
	specialeffect EF_SPELLBREAKER;
	specialeffect EF_BASH;
	.@random = rand(1, 4);
	for (.@i = 1; .@i <= .@random; ++.@i)
		makeitem Dark_Red_Jewel,1,"this",171,138;
	makeitem Coagulated_Spell,1,"this",171,138;
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("Strange crack#8");
	end;
OnEnable:
	enablenpc instance_npcname("Strange crack#8");
	end;
}

// Merchant
//============================================================
glast_01,210,273,5	script	Hugin's Buttler	1_F_04,{
	disable_items;
	if (MaxWeight - Weight < 1000) {
		mes "Your bag is too heavy. Reduce some weight and come back.";
		close;
	}
	mes "[Hugin's Buttler]";
	mes "Bring Temporal Crystal items for various Temporal boots items. Pick something you want.";
	next;
	switch (select("Cancel","Buy Temporal Boots (Need 1 Temporal Crystal)","Upgrade Temporal Boots (Need 5 Temporal Crystals)")) {
	case 1:
		mes "[Hugin's Buttler]";
		mes "Come back whenever you want.";
		close;
	case 2:
		if (countitem(Temporal_Crystal) > 0) {
			mes "[Hugin's Buttler]";
			mes "Take these Temporal Boots. We can upgrade your Temporal Boots with 5 Temporal Crystals.";
			delitem Temporal_Crystal, 1;
			getitem Temporal_Boots, 1;
			close;
		}
		mes "[Hugin's Buttler]";
		mes "You need more Temporal Crystals. Come back when you get some more Temporal Crystals during your travel through Old Glast Heim.";
		close;
	case 3:
		.@part = EQI_SHOES;
		if (!getequipisequiped(.@part)) {
			mes "[Hugin's Buttler]";
			mes "Only the item you are wearing can be upgraded.";
			mes "Wear the ^0000FFTemporal Boots^000000 you want to upgrade, and then talk to me once again.";
			close;
		}
		if (isequipped(2499)) {
			if (countitem(Temporal_Crystal) > 4) {
				mes "[Hugin's Buttler]";
				mes "You can trade ^0000FF1 Temporal Boots and 5 Temporal Crystals^000000 for 1 of the following items. ^FF0000Previous refine rate will not be retained with these new ones^000000.";
				next;
				setarray .@type$[0],"Strength","Intelligence","Agility","Vitality","Dexterity","Lucky";
				setarray .@equip_type[0],Temporal_STR_Boots,Temporal_INT_Boots,Temporal_AGI_Boots,Temporal_VIT_Boots,Temporal_DEX_Boots,Temporal_LUK_Boots;
				.@select = select("Cancel","Str Boots","Int Boots","Agi Boots","Vit Boots","Dex Boots","Luk Boots");
				switch (.@select) {
				case 1:
					mes "[Hugin's Buttler]";
					mes "Come back anytime you want.";
					close;
				case 2:
				case 3:
				case 4:
				case 5:
				case 6:
				case 7:
					mes "[Hugin's Buttler]";
					mes "Are you sure about buying Temporal ^FF0000"+.@type$[.@select-2]+"^000000 Boots? There's no refunds.";
					next;
					if (select("Think again","Yes, I am sure.") == 2) {
						mes "[Hugin's Buttler]";
						mes "The boots have been upgraded as you want.";
						delequip .@part;
						delitem Temporal_Crystal,5;
						getitem .@equip_type[.@select - 2],1;
						close;
					}
					mes "[Hugin's Buttler]";
					mes "Ok, come back again when you are sure.";
					close;
				}
			}
			mes "[Hugin's Buttler]";
			mes "Not enough Temporal Crystals. You need 5 Temporal Crystals to upgrade your item.";
			close;
		}
		mes "[Hugin's Buttler]";
		mes "You are not wearing the Temporal Boots. Talk to me once you're wearing them.";
		close;
	}
}

// Enchant and Socket NPC
//============================================================
glast_01,212,273,4	script	Hugin's Magic Master	1_F_01,{
	disable_items;
	if (MaxWeight - Weight < 1000) {
		mes "Your bag is too heavy. Reduce some weight and come back.";
		close;
	}
	mes "[Hugin's Magic Master]";
	mes "So you came to enchant the Temporal Boots. Be aware that Boots with sockets cannot be enchanted, and this upgrade is only possible for the boots you are wearing.";
	next;
	if (select("How does the enchanting work?","Give effect to my Temporal Boots") == 1) {
		mes "[Hugin's Magic Master]";
		mes "You can keep obtain ^0000FFCoagulated Spell^000000 through your travel times.";
		next;
		mes "[Hugin's Magic Master]";
		mes "I can enchant your Temporal Boots items depands on the amount of that item. ^0000FFNot normal type boots^000000. Only possible for those 6 expanded version boots.";
		next;
		mes "[Hugin's Magic Master]";
		mes "But, once you put a socket into the item, you cannot enchant it. Make sure to remember that.";
		next;
		mes "[Hugin's Magic Master]";
		mes "I don't give out effect randomly. You can pick which effect you want for the item.";
		next;
		mes "[Hugin's Magic Master]";
		mes "Of course it is for 4th slot only. You can have small effect at the start, and then the effect gets stronger.";
		next;
		mes "[Hugin's Magic Master]";
		mes "To get to the final option upgrade, it will require way more Coagulated Spell items. Don't forget that.";
		next;
		mes "[Hugin's Magic Master]";
		mes "Once the 4th slot completes the best option, you get to have a random bonus effect to the 3rd slot. With.. certain payment~";
		next;
		mes "[Hugin's Magic Master]";
		mes "Also, you don't lose the item even if the enchant fails, we also guarantee keeping card and refine rate. But we can't initialize enchanting.";
		next;
		mes "[Hugin's Magic Master]";
		mes "Phew...";
		mes "Anyways, that's all for basic enchant information. Now you should see for yourself.";
		close;
	}
	.@part = EQI_SHOES;
	if (!getequipisequiped(.@part)) {
		mes "[Hugin's Magic Master]";
		mes "Are you wearing the item?";
		close;
	}
	.@equip_id = getequipid(.@part);
	.@equip_name$ = getequipname(.@part);
	.@card3 = getequipcardid(.@part,3);
	setarray .@enchant1st[0],Fighting_Spirit4,Expert_Archer1,Spell2,Vitality2,Attack_Delay_1,Luck3;
	setarray .@enchant2nd[0],Fighting_Spirit5,Expert_Archer2,Spell3,Vitality3,Attack_Delay_2,Luck4;
	setarray .@enchant3rd[0],Fighting_Spirit6,Expert_Archer3,Spell4,MHP1,Attack_Delay_3,Luck5;
	setarray .@enchant4th[0],Fighting_Spirit7,Expert_Archer4,Spell5,MHP2,Attack_Delay_4,Luck6;
	setarray .@enchant_cost[0],1,4,15,30,10;
	switch (.@equip_id) {
	case 22000: //Temporal_STR_Boots
	case 22001: //Temporal_INT_Boots
	case 22002: //Temporal_AGI_Boots
	case 22003: //Temporal_VIT_Boots
	case 22004: //Temporal_DEX_Boots
	case 22005: //Temporal_LUK_Boots
		if (!.@card3) { //4th slot 1st try enchanting
			.@cost = .@enchant_cost[0];
			.@number = 1;
			mes "[Hugin's Magic Master]";
			mes "Want to enchant ^0000FF"+.@equip_name$+"^000000?";
			mes "For the "+.@number+"st enchanting, you need ^0000FF"+.@cost+"^000000 Coagulated Spell items.";
			next;
			.@select = select("Quit","Fighting Spirit","Archery","Spell","Vitality","Attack Speed","Lucky");
			switch (.@select) {
			case 1:
				mes "[Hugin's Magic Master]";
				mes "Ok, come back when you are ready.";
				close;
			case 2:
				.@enchant = .@enchant1st[.@select-2];
				break;
			case 3:
				.@enchant = .@enchant1st[.@select-2];
				break;
			case 4:
				.@enchant = .@enchant1st[.@select-2];
				break;
			case 5:
				.@enchant = .@enchant1st[.@select-2];
				break;
			case 6:
				.@enchant = .@enchant1st[.@select-2];
				break;
			case 7:
				.@enchant = .@enchant1st[.@select-2];
				break;
			}
		} else {
			if (.@card3 == .@enchant1st[0] || .@card3 == .@enchant1st[1] || .@card3 == .@enchant1st[2] ||
				.@card3 == .@enchant1st[3] || .@card3 == .@enchant1st[4] || .@card3 == .@enchant1st[5]) {
				.@cost = .@enchant_cost[1];
				.@number = 2;
			} else if (.@card3 == .@enchant2nd[0] || .@card3 == .@enchant2nd[1] || .@card3 == .@enchant2nd[2] ||
				.@card3 == .@enchant2nd[3] || .@card3 == .@enchant2nd[4] || .@card3 == .@enchant2nd[5]) {
				.@cost = .@enchant_cost[2];
				.@number = 3;
			} else if (.@card3 == .@enchant3rd[0] || .@card3 == .@enchant3rd[1] || .@card3 == .@enchant3rd[2] ||
				.@card3 == .@enchant3rd[3] || .@card3 == .@enchant3rd[4] || .@card3 == .@enchant3rd[5]) {
				.@cost = .@enchant_cost[3];
				.@number = 4;
			} else if (.@card3 == .@enchant4th[0] || .@card3 == .@enchant4th[1] || .@card3 == .@enchant4th[2] ||
				.@card3 == .@enchant4th[3] || .@card3 == .@enchant4th[4] || .@card3 == .@enchant4th[5]) {
				.@card2 = getequipcardid(.@part,2);
				.@cost = .@enchant_cost[4];
				if (.@card2) {
					mes "[Hugin's Magic Master]"; //Custom text
					mes "Your shoes have reached the maximum option to enchant.";
					mes "Please bring me a new one if you want me to enchant again.";
					close;
				}
				//3rd slot random enchanting
				switch (.@card3) {
					case 4822: //Fighting_Spirit7
					case 4835: //Expert_Archer4
					case 4826: //Spell5
					case 4862: //MHP2
					case 4881: //DelayafterAttack4Lv
					case 4755: //Luck6
						.@enchant = callfunc("F_Rand",Bear's_Power,Runaway_Magic,Speed_Of_Light,Muscle_Fool,Hawkeye,Lucky_Day);
						mes "[Hugin's Magic Master]"; //Custom text
						mes "Seems you already completed the best upgrade option in your 4th slot.";
						mes "For that I can give a random bonus effect to your 3rd slot. And of course, with certain payment...";
						next;
						mes "[Hugin's Magic Master]";
						mes "Enchanting ^0000FF"+.@equip_name$+"^000000's 3rd slot for obtaining a random bonus effect. Requires ^0000FF"+.@cost+"^000000 Coagulated Spell items.";
						next;
						if (select("Quit","Effect Upgrade!") == 1) {
							mes "[Hugin's Magic Master]";
							mes "Ok, come back when you are ready.";
							close;
						}
						if (countitem(Coagulated_Spell) < .@cost) {
							mes "[Hugin's Magic Master]";
							mes "Hmm, you are missing "+(.@cost - countitem(Coagulated_Spell))+" Coagulated Spell items. Go get more, and then we can talk about more enchants.";
							close;
						}
						specialeffect2 EF_REPAIRWEAPON;
						.@equip_refine = getequiprefinerycnt(.@part);
						delitem Coagulated_Spell,.@cost;
						delequip .@part;
						mes "[Hugin's Magic Master]";
						mes "This is it! This special stone is now yours.";
						mes "Hahaha... I am really the only master of these things.";
						mes "Thank you for believing in me.";
						getitem2 .@equip_id,1,1,.@equip_refine,0,0,0,.@enchant,.@card3;
						close;
				}
			}
			//4th slot 2nd try enchanting
			if (.@card3 == .@enchant1st[0])
				.@enchant = .@enchant2nd[0];
			else if (.@card3 == .@enchant1st[1])
				.@enchant = .@enchant2nd[1];
			else if (.@card3 == .@enchant1st[2])
				.@enchant = .@enchant2nd[2];
			else if (.@card3 == .@enchant1st[3])
				.@enchant = .@enchant2nd[3];
			else if (.@card3 == .@enchant1st[4])
				.@enchant = .@enchant2nd[4];
			else if (.@card3 == .@enchant1st[5])
				.@enchant = .@enchant2nd[5];
			//4th slot 3rd try enchanting
			if (.@card3 == .@enchant2nd[0])
				.@enchant = .@enchant3rd[0];
			else if (.@card3 == .@enchant2nd[1])
				.@enchant = .@enchant3rd[1];
			else if (.@card3 == .@enchant2nd[2])
				.@enchant = .@enchant3rd[2];
			else if (.@card3 == .@enchant2nd[3])
				.@enchant = .@enchant3rd[3];
			else if (.@card3 == .@enchant2nd[4])
				.@enchant = .@enchant3rd[4];
			else if (.@card3 == .@enchant2nd[5])
				.@enchant = .@enchant3rd[5];
			//4th slot 4th try enchanting
			if (.@card3 == .@enchant3rd[0])
				.@enchant = .@enchant4th[0];
			else if (.@card3 == .@enchant3rd[1])
				.@enchant = .@enchant4th[1];
			else if (.@card3 == .@enchant3rd[2])
				.@enchant = .@enchant4th[2];
			else if (.@card3 == .@enchant3rd[3])
				.@enchant = .@enchant4th[3];
			else if (.@card3 == .@enchant3rd[4])
				.@enchant = .@enchant4th[4];
			else if (.@card3 == .@enchant3rd[5])
				.@enchant = .@enchant4th[5];
			mes "[Hugin's Magic Master]";
			mes "Enchanting ^0000FF"+.@equip_name$+"^000000's 4th slot as ^0000FF"+.@number+"^000000 level effect. Requires ^0000FF"+.@cost+"^000000 Coagulated Spell items.";
			next;
			if (select("Quit","Effect Upgrade!") == 1) {
				mes "[Hugin's Magic Master]";
				mes "Ok, come back when you are ready.";
				close;
			}
			if (countitem(Coagulated_Spell) < .@cost) {
				mes "[Hugin's Magic Master]";
				mes "Hmm, you are missing "+(.@cost - countitem(Coagulated_Spell))+" Coagulated Spell items. Go get more, and then we can talk about more enchants.";
				close;
			}
		}
		break;
	default:
		mes "[Hugin's Magic Master]";
		mes "This is not the right item for this enchant.";
		mes "Remember, only those 6 types of Temporal Boots are available for enchanting.";
		close;
	}
	specialeffect2 EF_REPAIRWEAPON;
	.@equip_refine = getequiprefinerycnt(.@part);
	delitem Coagulated_Spell,.@cost;
	delequip .@part;
	mes "[Hugin's Magic Master]";
	mes "Trying for enchant number ^630000"+.@number+"^000000.";
	getitem2 .@equip_id,1,1,.@equip_refine,0,0,0,0,.@enchant;
	close;
}

glast_01,210,270,0	script	Hugin's Craftsman	4_F_JOB_BLACKSMITH,{
	disable_items;
	if (MaxWeight - Weight < 1000) {
		mes "Your bag is too heavy. Reduce some weight and come back.";
		close;
	}
	mes "[Hugin's Craftsman]";
	mes "That Magic Master always says that enchanting Temporal Boots is the best. But, I think quite different.";
	next;
	mes "[Hugin's Craftsman]";
	mes "I think putting a slot into the item would be the best. Even with some risk of failing.";
	next;
	mes "[Hugin's Craftsman]";
	mes "Bring me ^0000FF5 Temporal Crystals^000000, and I will try to put a slot into your advanced Temporal Boots. But, it's possible to ^FF0000fail and break the item^000000. Is that ok?";
	next;
	if (select("Cancel","Take the risk and try to add a slot") == 1) {
		mes "[Hugin's Craftsman]";
		mes "Ok, come back anytime you want.";
		close;
	}
	.@part = EQI_SHOES;
	if (!getequipisequiped(.@part)) {
		mes "[Hugin's Craftsman]";
		mes "Are you sure you are wearing the item?";
		close;
	}
	.@equip_id = getequipid(.@part);
	switch (.@equip_id) {
	case 22000: //Temporal_STR_Boots
	case 22001: //Temporal_INT_Boots
	case 22002: //Temporal_AGI_Boots
	case 22003: //Temporal_VIT_Boots
	case 22004: //Temporal_DEX_Boots
	case 22005: //Temporal_LUK_Boots
		mes "[Hugin's Craftsman]";
		mes "And ^FF0000all enchant or refine rates will be lost if you fail this.^000000";
		mes "Still want to risk it?";
		next;
		if (select("Cancel","I am ok with it!") == 1) {
			mes "[Hugin's Craftsman]";
			mes "Ok, come back anytime you want.";
			close;
		}
		mes "[Hugin's Craftsman]";
		mes "Once again, ^FF0000That Magic Master won't enchant any item with a slot.^000000";
		mes "Still want to proceed?";
		next;
		if (select("Cancel","I understand. Try to slot it.") == 1) {
			mes "[Hugin's Craftsman]";
			mes "Ok, come back anytime you want.";
			close;
		}
		if (countitem(Temporal_Crystal) < 5) {
			mes "[Hugin's Craftsman]";
			mes "You must bring ^0000FF5 Temporal Crystal items^000000. Nothing is free you know...";
			close;
		}
		.@random = rand(1,100);
		if (.@random <= 80) { //Custom rates
			mes "[Hugin's Craftsman]";
			mes "Arrggg, we failed. Better luck next time.";
			specialeffect2 EF_PHARMACY_FAIL;
			delitem Temporal_Crystal,5;
			delequip .@part;
			close;
		}
		mes "[Hugin's Craftsman]"; //Custom text
		mes "Yuhuu~ we succeed. Here you are...";
		mes "A slot has been added to your advanced Temporal Boots.";
		mes "Thank you for believing in me.";
		specialeffect2 EF_PHARMACY_OK;
		delitem Temporal_Crystal,5;
		delequip .@part;
		if (.@equip_id == Temporal_STR_Boots) {
			getitem Temporal_STR_Boots_,1;
		} else if (.@equip_id == Temporal_INT_Boots) {
			getitem Temporal_INT_Boots_,1;
		} else if (.@equip_id == Temporal_AGI_Boots) {
			getitem Temporal_AGI_Boots_,1;
		} else if (.@equip_id == Temporal_VIT_Boots) {
			getitem Temporal_VIT_Boots_,1;
		} else if (.@equip_id == Temporal_DEX_Boots) {
			getitem Temporal_DEX_Boots_,1;
		} else if (.@equip_id == Temporal_LUK_Boots) {
			getitem Temporal_LUK_Boots_,1;
		}
		close;
	default:
		mes "[Hugin's Craftsman]";
		mes "This is not it! We need ^0000FFAdvanced Temporal Boots^000000 items! The item ^0000FFwith a stat effect and without a slot^000000!";
		close;
	}
}

// Glast Heim Nightmare Mode :: nightmare_gh
//============================================================
glast_01,195,273,4	script	Hugin's Follower#n_gh	2_F_MAGICMASTER,{
	mes "[Hugin's Follower]";
	mes "I can't move between time and dimension like a master Hugin but I can pretend to move.";
	next;
	mes "[Hugin's Follower]";
	mes "What about this Glast Heim is born with powerful dark force?";
	next;
	if (select("I don't care","That is interesting, please tell me more") == 1) {
		mes "[Hugin's Follower]";
		mes "Well~ If you are not interested in it, never mind.";
		close;
	}
	mes "[Hugin's Follower]";
	mes "As learning ability from Hugin, I just find out the path to Old Glast Heim of more powerful dark force.";
	next;
	mes "[Hugin's Follower]";
	mes "How is it? Do you want to explore there?";
	next;
	if (select("No, I want to stop it","I really want to do that!") == 1) {
		mes "[Hugin's Follower]";
		mes "Well~ If you are not interested in it, never mind.";
		close;
	}
	mes "[Hugin's Follower]";
	mes "Sure that is not free~ I have to get something little bit. Hoho...";
	next;
	mes "[Hugin's Follower]";
	mes "I can send you just two place. How is it? Do you want to try?";
	next;
	switch (select("Cancel","Monastery Cemetery (Lv100~130) - 5000z","Corridor of 2nd floor (Over Lv140) - 10000z")) {
	case 1:
		mes "[Hugin's Follower]";
		mes "Alright, please come back whenever you change mind. I think you will have lots of thing to talk with me...";
		close;
	case 2:
		if (Zeny < 5000) {
			mes "[Hugin's Follower]";
			mes "Please make sure you already have all of the money.";
			close;
		}
		Zeny -= 5000;
		warp "gl_chyard_",148,144;
		end;
	case 3:
		if (Zeny < 10000) {
			mes "[Hugin's Follower]";
			mes "Please make sure you already have all of the money.";
			close;
		}
		Zeny -= 10000;
		warp "gl_cas02_",104,31;
		end;
	}
}

gl_chyard_,148,149,4	script	Hugin's Follower#n_gh1	2_F_MAGICMASTER,{
	mes "[Hugin's Follower]";
	mes "You want to break?";
	mes "Ok, let's get out of here.?";
	next;
	if (select("Ah, wrong click... I mean I was not talk to you","Please let me out from here") == 1) {
		mes "[Hugin's Follower]";
		mes "Alright just tell me whenever you want to leave.";
		close;
	}
	warp "glast_01",210,269;
	end;
}

gl_cas02_,104,36,4	script	Hugin's Follower#n_gh2	2_F_MAGICMASTER,{
	mes "[Hugin's Follower]";
	mes "You want to break?";
	mes "Ok, let's get out of here.?";
	next;
	if (select("Ah, wrong click... I mean I was not talk to you","Please let me out from here") == 1) {
		mes "[Hugin's Follower]";
		mes "Alright just tell me whenever you want to leave.";
		close;
	}
	warp "glast_01",210,269;
	end;
}
